Class TSP_TitleHandler : EventHandler
{
	int titleFrame;
	int bgFrame;
	float bgFade;
	
	const TITLEEND = 150;
	const BGEND = 100;
	
	ui double easeIt (double x)
	{
		return x < 0.5 ? 8 * x * x * x * x : 1 - ((-2 * x + 2) ** 4) / 2;
	}
	
	ui double ease_inOutSine (double x)
	{
		return -( cos(180 * x) - 1 ) / 2.;
	}
	
	override void WorldLoaded(WorldEvent e)
	{
		titleFrame = -12;
		bgFade = 0.0;
	}
	
	override void WorldTick()
	{
		if ( titleFrame < TITLEEND )
		{
			titleFrame++;
		}
		if ( bgFrame < BGEND )
		{
			bgFrame++;
		}
		
		if ( titleFrame >= BGEND && bgFade < 1.0 )
		{
			bgFade += 0.02;
		}
	}
	
	override void RenderOverlay(RenderEvent e)
	{
		Shader.SetUniform1f(players[consoleplayer], "TSP_TitleShader", "text_fadeAmount", ease_inOutSine(max(0.0, titleFrame / double(TITLEEND))));
		Shader.SetUniform1f(players[consoleplayer], "TSP_TitleShader", "textbg_fadeAmount", ease_inOutSine(max(0.0, bgFrame / double(BGEND))));
		Shader.SetUniform1f(players[consoleplayer], "TSP_TitleShader", "bg_fadeAmount", bgFade);
	}
}