Extend Class TSP_PlayerPawn_ZSCBase
{	
	bool tsp_crouchSliding;
	
	double tsp_bob;
	double tsp_lastbob;
	double tsp_bobamt;
	
	override void MovePlayer ()
	{
		UserCmd cmd = player.cmd;
		
		Angle += cmd.yaw * (360./65536.);
		
		if(pp_hitFreeze>0)
		{
			A_SetAngle(angle-1,0);
			A_SetAngle(angle+1,SPF_INTERPOLATE);
			A_SetPitch(pitch-1,0);
			A_SetPitch(pitch+1,SPF_INTERPOLATE);
			return;
		}
		
		if ( player.cmd.buttons & BT_JUMP || ( tsp_crouchSliding && vel.xy.Length() < 2 ) )
		{
			tsp_crouchSliding = false;
		}
		else if ( player.cmd.buttons & BT_CROUCH )
		{
			if ( !tsp_crouchSliding )
			{
				vel.z -= 4;
				vel.xy += (cos(angle), sin(angle)) * 6;
			}
			tsp_crouchSliding = true;
		}
		
		bNOFRICTION = tsp_crouchSliding;

		player.onground = (pos.z <= floorz) || bOnMobj || bMBFBouncer || (player.cheats & CF_NOCLIP2);
		
		if ( tsp_crouchSliding && player.onground )
		{
			vel.xy *= 0.99;
		}
		else if ( player.onground )
		{
			vel.xy *= 0.9;
		}
		
		Vector2 moveDir = (cmd.forwardmove, -cmd.sidemove) / 6400;
		double moveAngle = atan2(moveDir.y, moveDir.x) + angle;
		
		double runspeed = min(moveDir.Length()*2, 3);
		//runspeed *= Speed;
		
		bool isMoving = (cmd.forwardmove || cmd.sidemove);
		
		if ( !tsp_crouchSliding && (cmd.forwardmove || cmd.sidemove) )
		{
			vel.xy += (cos(moveAngle), sin(moveAngle)) * ( !player.onground ? 0.1 : runspeed );
		}
		
		//int stepFreq = 30 / runspeed;
		double bobTarg = cos(level.maptime * runspeed * 8) * tsp_bobAmt;//(stepFreq - (level.maptime % stepFreq)) / double(stepFreq) * tsp_bobAmt;
		tsp_lastbob = tsp_bob;
		tsp_bob = tsp_bob + ( bobTarg * 2 - tsp_bob ) / 3;
		tsp_bobamt = tsp_bobamt + ( (isMoving ? 1 : 0) - tsp_bobAmt ) / 4.;
	}	
	
	override void CalcHeight()
	{
		if ( tsp_crouchSliding )
		{
			player.viewz = pos.Z + 24;
		}
		else
		{
			player.viewz = pos.Z + viewheight;
			//super.CalcHeight();
		}
	}
	
	override void CheckPitch()
	{
		if(pp_hitFreeze>0)
		{
			return;
		}
		super.CheckPitch();
	}
}