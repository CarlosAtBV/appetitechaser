Class TSP_NotificationLogger
{
	string message;
	int time;
}

Class TSP_StatusBar : BaseStatusBar
{
	HUDFont mHUDFont;
	HUDFont mSmallFont;
	HUDFont mAmountFont;
	HUDFont mTinyFont;
	
	HUDFont fnt_ammoBig;
	HUDFont fnt_ammoSmall;
	HUDFont fnt_ammoTag;
	
	InventoryBarState diparms;
	LinearValueInterpolator mHealthInterpolator;
	LinearValueInterpolator mShieldInterpolator;
	LinearValueInterpolator mChargeInterpolator;
	
	int hud_money_target;
	int hud_money_lerp;
	
	TSP_PlayerPawn_ZSCBase tsp_player;
	
	double mTraitAlpha;
	
	bool isDebug;
	
	Vector2 screenSize;
	
	const CLR_WHITE = Color(255, 255, 255, 255);
	const CLR_AMMO_LOW = Color(255, 255, 0, 0);
	const CLR_AMMO_NORMAL = Color(255, 255, 213, 144);
	const CLR_CLIP_NORMAL = Color(255, 255, 255, 208);
	const CLR_CLIP_LOW = Color(255, 255, 179, 66);
	
	Array<TSP_NotificationLogger> tsp_notifs;
	
	override bool ProcessNotify(EPrintLevel printlevel, String outline)
	{
		if(level.maptime==0) return true;
		if(printlevel == PRINT_MEDIUM||printlevel == PRINT_HIGH)
		{
			TSP_NotificationLogger notif = new("TSP_NotificationLogger");
			notif.message = outline;
			notif.time = 4*35;
			tsp_notifs.Insert(0,notif);
			
			if(tsp_notifs.Size()>4)
			{
				tsp_notifs.Delete(tsp_notifs.Size()-1);
			}
		}
		return true;
	}

	override void Init()
	{
		Super.Init();
		SetSize(0, 1280, 720);

		// Create the font used for the fullscreen HUD
		Font fnt = "TSP_HUDFONT";
		mHUDFont = HUDFont.Create(fnt,1,false);
		fnt = "SmallFont";
		mSmallFont = HUDFont.Create(fnt);
		mTinyFont = HUDFont.Create("TINYFONT",0,Mono_off,1,1);
		mAmountFont = HUDFont.Create("INDEXFONT");
		fnt_ammoBig = HUDFont.Create("tsp_ammo_big");
		fnt_ammoSmall = HUDFont.Create("tsp_ammo_small");
		fnt_ammoTag = HUDFont.Create("tsp_ammo_tag");
		
		diparms = InventoryBarState.Create();
		mHealthInterpolator = LinearValueInterpolator.Create(0, 8);
		mShieldInterpolator = LinearValueInterpolator.Create(0, 8);
		mChargeInterpolator = LinearValueInterpolator.Create(0, 1);
	}
	
	override void Tick()
	{
		super.Tick();
		mHealthInterpolator.Update(CPlayer.health);
		mShieldInterpolator.Update(CPlayer.mo.CountInv("TSP_PlayerPawn_Shield"));
		//mMoneyInterpolator.Update(CPlayer.mo.CountInv("TSP_GlobalDollars"));
		hud_money_target = CPlayer.mo.CountInv("TSP_GlobalDollars");
		
		if ( hud_money_lerp != hud_money_target )
		{
			hud_money_lerp = hud_money_lerp + ( hud_money_target - hud_money_lerp ) * 0.99;
			if ( (hud_money_target - hud_money_lerp) < 10 )
			{
				hud_money_lerp = hud_money_target;
			}
		}
		
		mChargeInterpolator.Update(CPlayer.mo.CountInv("TSPChargedAttack"));
		
		tsp_player = TSP_PlayerPawn_ZSCBase(CPlayer.mo);
		
		mTraitAlpha = mTraitAlpha + (tsp_player.pp_traitMode - mTraitAlpha)/4;
		
		if(level.maptime%35==0)
		{
			isDebug = CVar.GetCVar("tsp_debug",players[consoleplayer]).GetBool();
		}
		
		for(int i = 0;i<tsp_notifs.Size();i++)
		{
			tsp_notifs[i].time--;
			if(tsp_notifs[i].time<=0)
			{
				tsp_notifs.Delete(i);
			}
		}
	}
	
	const HUDLEFT_X = 0;
	const HUDLEFT_Y = -140;
	const HUDRIGHT_X = -270;
	const HUDRIGHT_Y = -140;

	override void Draw (int state, double TicFrac)
	{
		Super.Draw (state, TicFrac);
		
		let curWeapon = TSP_Weapon_Core(CPlayer.ReadyWeapon);
		
		if ( curWeapon )
		{
			curWeapon.TSP_DrawWeaponHud(TicFrac);
		}
		
		let tsp_player = TSP_PlayerPawn_ZSCBase(CPlayer.mo);
		
		if ( tsp_player )
		{
			float hitTime = tsp_player.pp_hitMult;
			drawOffset = (hitTime > 0) ? (round(frandom(-5,5) * hitTime), round(frandom(-5,5) * hitTime)) : (0, 0);
		}

		if (state == HUD_StatusBar||state == HUD_Fullscreen)
		{
			BeginHUD(1.0, true);//,true,320,240);
			
			Vector2 hudScale = GetHUDScale();
			screenSize = ( screen.GetWidth()/hudScale.x, screen.GetHeight()/hudScale.y );
			
			alpha = 1.0;
			
			int health = mHealthInterpolator.GetValue();
			int shield = mShieldInterpolator.GetValue();
			int money = hud_money_lerp;
			int maxShield = CPlayer.mo.CountInv("TSP_PlayerPawn_MaxShield");
			int charge = mChargeInterpolator.GetValue();
            //DrawImage("H_GUIDEL", (0, -140), DI_ITEM_OFFSETS); // guide
            //DrawImage("H_GUIDER", (-270, -140), DI_ITEM_OFFSETS); // guide
			
			//LEFT SIDE OF THE HUD (Health, Armor, Armor Pips, Money)
			
			// SHADOW
			DrawImage("HUD_LBG", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS);
			
			// MEL HUD
			if(CPlayer.mo is "TSP_MelPlayer" ||CPlayer.mo is "TSP_MelRetroPlayer")
			{
				DrawImage("HUD_LMEL", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS);
			}
			
			//DrawString(mSmallFont, FormatNumber(money, 180000), (HUDLEFT_X, HUDLEFT_Y+25), DI_TEXT_ALIGN_LEFT|DI_NOSHADOW, Font.CR_WHITE);
			
			if(CPlayer.mo.InvSel)
			{
				DrawImage("HUD_IBOX", (0, -16), DI_ITEM_CENTER|DI_SCREEN_CENTER_BOTTOM);
				DrawInventoryIcon(CPlayer.mo.InvSel,(0, -16),DI_ITEM_CENTER|DI_SCREEN_CENTER_BOTTOM,0.5);
				//DrawString(mSmallFont, CPlayer.mo.InvSel.GetTag().." ("..CPlayer.mo.InvSel.Amount..")", (HUDLEFT_X, HUDLEFT_Y-40), DI_TEXT_ALIGN_LEFT|DI_NOSHADOW, Font.CR_WHITE);
			}
			
			// Main Stats
			
			
			// HEALTH
				Color hpColor;
				
				bool overheal = (health > 100);
				int healthWidth = 121;
				int healthOffset_base = ( health / 100. ) * healthWidth;
				
				string healthBarTex;
				
				// [Py] Get the best graphic for the healthbar depending on the amount of HP
				if ( overheal )
				{
					healthBarTex = "HUD_VITO";
					healthOffset_base -= healthWidth;
				}
				else if ( health>70 )
				{
					healthBarTex = "HUD_VITF";
				}
				else if ( health>40 )
				{
					healthBarTex = "HUD_VITM";
				}
				else
				{
					healthBarTex = "HUD_VITR";
				}
				
				// [Py] Draw the background
				// [Py] This swaps over to VITF for the overheal so that we only have to write this one line because uhhhhhhh
				DrawImage(overheal ? "HUD_VITF" : "HUD_VITE", (HUDLEFT_X+122, HUDLEFT_Y+75), DI_ITEM_LEFT_TOP|DI_SCREEN_LEFT_BOTTOM);
				
				if ( health > 0 )
				{
					// [Py] just a mini fucked up hack for masking the bars ...
					for ( int i = 0; i < 3; i++ )
					{
						switch ( i )
						{
							case 0: SetClipRect(HUDLEFT_X+122, HUDLEFT_Y+75, 123, 6); break;
							case 1: SetClipRect(HUDLEFT_X+125, HUDLEFT_Y+81, 125, 6); break;
							case 2: SetClipRect(HUDLEFT_X+128, HUDLEFT_Y+87, 125, 6); break;
						}
						DrawImage(healthBarTex, (HUDLEFT_X+121 + (-healthWidth + healthOffset_base), HUDLEFT_Y+75), DI_ITEM_LEFT_TOP|DI_SCREEN_LEFT_BOTTOM);
					}
					ClearClipRect();
				}
				

			// SHIELD BAR
			switch(CPlayer.mo.CountInv("TSP_Upgrade_Shield"))
			{
				case 0: DrawBar("HUD_SHD0", "HUD_SHDE", shield, maxShield, (HUDLEFT_X+122, HUDLEFT_Y+95), 0, 0, DI_ITEM_OFFSETS); break;
				case 1: DrawBar("HUD_SHD1", "HUD_SHDE", shield, maxShield, (HUDLEFT_X+122, HUDLEFT_Y+95), 0, 0, DI_ITEM_OFFSETS); break;
				case 2: DrawBar("HUD_SHD2", "HUD_SHDE", shield, maxShield, (HUDLEFT_X+122, HUDLEFT_Y+95), 0, 0, DI_ITEM_OFFSETS); break;
				case 3: DrawBar("HUD_SHD3", "HUD_SHDE", shield, maxShield, (HUDLEFT_X+122, HUDLEFT_Y+95), 0, 0, DI_ITEM_OFFSETS); break;
				case 4: DrawBar("HUD_SHD4", "HUD_SHDE", shield, maxShield, (HUDLEFT_X+122, HUDLEFT_Y+95), 0, 0, DI_ITEM_OFFSETS); break;
				case 5: DrawBar("HUD_SHD5", "HUD_SHDE", shield, maxShield, (HUDLEFT_X+122, HUDLEFT_Y+95), 0, 0, DI_ITEM_OFFSETS); break;
			}
			
			// MAIN HUD GRAPHIC (LEFT)
			DrawImage("HUD_LBAS", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS);
			
			// MONEY
			DrawImage("HUD_LMON", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS); // Money Box
			//DrawString(fnt_ammoTag, ""..money, (HUDLEFT_X+224, HUDLEFT_Y+49), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW, Font.CR_UNTRANSLATED);
			TSP_DrawString(fnt_ammoTag, ""..money, (HUDLEFT_X+224, HUDLEFT_Y+49), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW);
			
			
			//DrawString(mSmallFont, "Test", (HUDLEFT_X+224, HUDLEFT_Y+2), DI_NOSHADOW, Font.CR_UNTRANSLATED);
			//TSP_DrawString(mSmallFont,  "Test", (HUDLEFT_X+224, HUDLEFT_Y+2), DI_NOSHADOW, Font.CR_UNTRANSLATED);
			//DrawString(mSmallFont, FormatNumber(money, 180000), (HUDLEFT_X, HUDLEFT_Y+25), DI_TEXT_ALIGN_LEFT|DI_NOSHADOW, Font.CR_WHITE); //Money Stuff
			
			// SHIELD PIPS
			if(CPlayer.mo is "TSP_MelPlayer")
			{
				DrawImage("SHD_3A", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS);
			}
			if(CPlayer.mo is "TSP_MelRetroPlayer")
			{
				DrawImage("SHD_2A", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS);
			}
			switch(CPlayer.mo.CountInv("TSP_Upgrade_Shield"))
			{
				case 1: DrawImage("SHD_1B", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS); break;
				case 2: DrawImage("SHD_2B", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS); break;
				case 3: DrawImage("SHD_3B", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS); break;
				case 4: DrawImage("SHD_4B", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS); break;
				case 5: DrawImage("SHD_5B", (HUDLEFT_X, HUDLEFT_Y), DI_ITEM_OFFSETS); break;
			}
			
			//================
			// MUGSHOTS
			//================
			
			// MEL (MEL, RETROGRADE MEL)

			switch(CVar.GetCVar("tsp_melhudmod", players[0]).GetInt())
			{
				case 1: 
					DrawTexture(GetMugShot(7, MugShot.CUSTOM, "MC1"), (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break;// Default Mel
				case 2:
					DrawTexture(GetMugShot(7, MugShot.CUSTOM, "MR1"), (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break;// Retrograde Mel
				case 3: 
					DrawTexture(GetMugShot(7, MugShot.CUSTOM, "RR1"), (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break;// Retro/Robo Mel
				case 4: 
					DrawImage("MELOBYPA", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break;							   // Meloby
				default:		
					if(CPlayer.mo is "TSP_MelRetroPlayer") //RETROGRADE MEL
					{
						if(cplayer.getgender()==2) // Are you a robot?
						{
						DrawTexture(GetMugShot(7, MugShot.CUSTOM, "RR1"), (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS);
						}
						else // You are not a robot
						{
						DrawTexture(GetMugShot(7), (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS);
						}
					}
					
					else if(CPlayer.mo is "TSP_DummyPlayer") // Are you a dummy?
					{
						DrawImage("MELOBYPA", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS);
						//DrawString(mSmallFont, "DUMMY", (HUDLEFT_X+35, HUDLEFT_Y+30), DI_TEXT_ALIGN_CENTER|DI_NOSHADOW, Font.CR_YELLOW);
					}
					else // Draw default player mugshot
					{
						DrawTexture(GetMugShot(7), (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS);
					}
					

					if(CPlayer.mo is "TSP_MelPlayer")
					{
						/*if(CVar.GetCVar("tsp_supersecret", players[0]).GetBool())
						{
						DrawImage("MCWSTZZ", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS);
						}*/
					}
				
			}

			//================
			// GOD MODE SPARKS (Mel/Retro Mel)
			//================
			
			if(CPlayer.cheats & (CF_GODMODE | CF_GODMODE2) || CPlayer.mo.bInvulnerable)
			{
				switch(CVar.GetCVar("tsp_melhudmod", players[0]).GetInt())
				{
					case 1: 
						DrawImage("HUDSPA0", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break; // Green Sparks (Default Mel)
					case 2:
						DrawImage("HUDSPA0", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break; // Green Sparks (Retrograde Mel)
					case 3: 
						DrawImage("HUDSPB0", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); break; // Pink Sparks  (Retro/Robo Mel)
					default:
								if(CPlayer.mo is "TSP_MelRetroPlayer" && cplayer.getgender()==2) // Are you Robo/Retro Mel?
								{
								DrawImage("HUDSPB0", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); // Pink Sparks
								}
								else // You are not a robot
								{
								DrawImage("HUDSPA0", (HUDLEFT_X+20, HUDLEFT_Y-12), DI_ITEM_OFFSETS); // Green Sparks
								}
							
				}
			}
			
			//================
			// Keys
			//================
			
				Vector2 keypos = (HUDRIGHT_X+22, HUDRIGHT_Y+21);
				int rowc = 0;
				double roww = 0;
				for(let i = CPlayer.mo.Inv; i != null; i = i.Inv) //go thru the player's inventory to find some keys
				{
					if (i is "Key" && i.Icon.IsValid())
					{
						DrawTexture(i.Icon, keypos, DI_ITEM_LEFT_BOTTOM);
						Vector2 size = TexMan.GetScaledSize(i.Icon);
						keypos.X += size.X + 2;
						roww = max(roww, size.X);
					}
				}

			// RIGHT HAND SIDE
			
			// HOW CAN I KILL NOOBS WITHOUT ANY AMMO
			
			//================
			// RIGHT SIDE BASE
			//================
			
			// SHADOW
			DrawImage("HUD_RBG", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
			
			//================
			// TRAIT
			//================
			
			DrawImage("MEL_TRTE", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
			DrawImage("MEL_TRTF", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS, mTraitAlpha);
			
			
			// MEL SHENANIGANS
			int maxCharge;
			
			// BARE KNUCKLE - Stasis Uppercut
			if(CPlayer.mo.FindInventory("MelMelee") && CPlayer.mo.CountInv("TSPPrimaryCombo")==1)
			{
				maxCharge = 9;
			}
			
			if(CPlayer.ReadyWeapon && CPlayer.ReadyWeapon is "MelMelee")
			{
				DrawBar("MEL_HAGF", "MEL_HAGE", shield, maxShield, (HUDRIGHT_X+3, HUDRIGHT_Y+37), 0, 1, DI_ITEM_OFFSETS);
				DrawBar("MEL_HCHB", "MEL_HCHE", charge, maxCharge, (HUDRIGHT_X+3, HUDRIGHT_Y+46), 0, 1, DI_ITEM_OFFSETS);
				DrawImage("MEL_WEPB", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
			}
			
			
			if(CPlayer.ReadyWeapon && CPlayer.ReadyWeapon is "MelChargeStyle")
			{
				if(CPlayer.mo.CountInv("TSPPrimaryCombo")==1)
				{
					maxCharge = 6;
				}
				else if(CPlayer.mo.CountInv("TSPPrimaryCombo")==2)
				{
					maxCharge = 5;
				}
				else if(CPlayer.mo.CountInv("TSPAlternateCombo")==3)
				{
					maxCharge = 8;
				}
			}
			
			
			if(CPlayer.ReadyWeapon && CPlayer.ReadyWeapon is "MelChargeStyle")
			{
				DrawBar("MEL_HAGF", "MEL_HAGE", shield, maxShield, (HUDRIGHT_X+3, HUDRIGHT_Y+37), 0, 1, DI_ITEM_OFFSETS);
				DrawBar("MEL_HCHC", "MEL_HCHE", charge, maxCharge, (HUDRIGHT_X+3, HUDRIGHT_Y+46), 0, 1, DI_ITEM_OFFSETS);
				DrawImage("MEL_WEPC", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
			}
		

			//================
			// WEAPON BASE
			//================
			
			Inventory a1 = GetCurrentAmmo();
			
			// MAIN HUD GRAPHIC (AMMO)
			if (a1 != null)
			{
				DrawImage("HUD_RBGW", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
			}				

			// MAIN HUD GRAPHIC (RIGHT)
			DrawImage("HUD_RBAS", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
			
			// Draw Weapon Slots
			
			int slotCount = 0;
			int selSlotOffset = -1;
			Color selSlotColor;
			
			int slot_offset = 0;
			bool wepFound; int wepSlot;
			[wepFound, wepSlot] = CPlayer.weapons.LocateWeapon(CPlayer.ReadyWeapon.GetClass());
			
			for ( int i = 1; i < 10; i++ )
			{
				int slotSize = CPlayer.weapons.SlotSize(i);
				if ( slotSize > 0 )
				{
					int xPos = HUDRIGHT_X + 31 + (16*slot_offset);// + (19*slot_offset);
					
					if ( wepSlot == i )
					{
						selSlotOffset = xPos;
					}
					
					if ( CPlayer.HasWeaponsInSlot(i) )
					{
						let weaponRef = TSP_Weapon_Core(GetDefaultByType(CPlayer.weapons.GetWeapon(i, slotSize-1)));
						
						if ( weaponRef )
						{
							Color daColor = weaponRef.tspw_hudcolor;
							if ( wepSlot == i )
							{
								selSlotColor = daColor;
							}
							
							DrawImage("ARM_BOXB", (xPos, HUDRIGHT_Y + 48), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM, col: Color(255, daColor.r, daColor.g, daColor.b));
						}
					}
					DrawImage("ARM_BOXA", (xPos, HUDRIGHT_Y + 48), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM);
					DrawImage("ARM_NUM"..(i), (xPos, HUDRIGHT_Y + 48), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM);
					slot_offset += 1;
				}
			}
			
			if ( selSlotOffset != -1 )
			{
				DrawImage("ARM_BOXD", (selSlotOffset-2, HUDRIGHT_Y + 43), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM, col: Color(255, selSlotColor.r, selSlotColor.g, selSlotColor.b));
				DrawImage("ARM_BOXC", (selSlotOffset-2, HUDRIGHT_Y + 43), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM);
				DrawImage("ARM_NUM"..(wepSlot), (selSlotOffset+1, HUDRIGHT_Y + 45), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM);
			}
			
			DrawImage("ARM_ACCT", (HUDRIGHT_X + 27, HUDRIGHT_Y + 48), DI_ITEM_OFFSETS|DI_SCREEN_RIGHT_BOTTOM);
			
			// Draw Ammo Numbers
			
			if (a1 != null)
			{
				if(CPlayer.ReadyWeapon is "TSP_Weapon_Core")
				{
					TSP_Weapon_Core wepon = TSP_Weapon_Core(CPlayer.ReadyWeapon);
					
					DrawString(fnt_ammoTag, wepon.tspw_altname, (HUDRIGHT_X+149, HUDRIGHT_Y+98), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW, Font.CR_UNTRANSLATED);
				
					if(wepon.tspw_clipsize>0)
					{
						int ammoRed = wepon.tspw_clipsize*0.4;
						Color ammoColor;
						if(TSP_Weapon_Core(CPlayer.ReadyWeapon).tspw_curclip<ammoRed)
						{
							ammoColor = Font.FindFontColor("TSPHudRed");
						}
						else
						{
							ammoColor = Font.FindFontColor("TSPWhite");
						}
						
						
						int maxClip = TSP_Weapon_Core(CPlayer.ReadyWeapon).GetClipCapacity();
						int clipAmount = TSP_Weapon_Core(CPlayer.ReadyWeapon).tspw_curclip;
						float clipLeft = clipAmount / float(maxClip);
						Color baseColor = CLR_CLIP_NORMAL;
						Color lowColor = CLR_CLIP_LOW;
						Color clipColor = Color(
							255,
							int(lowColor.R + (baseColor.R - lowColor.R) * clipLeft),
							int(lowColor.G + (baseColor.G - lowColor.G) * clipLeft),
							int(lowColor.B + (baseColor.B - lowColor.B) * clipLeft)
						);
						string clipString = string.format("%03d", clipAmount);
						for ( int i; i < clipString.length(); i++ )
						{
							TSP_DrawString(fnt_ammoBig, clipString.mid(i, 1), (HUDRIGHT_X+72-4 + 21*i, HUDRIGHT_Y+75), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW,
								a1.amount == 0 ? CLR_AMMO_LOW : clipColor,
								alpha = (clipAmount == 0 || (""..clipAmount).length() < 3-i) ? 0.25 : 1.0);
						}
						
						int ammoAmount = a1.amount;
						string ammoString = string.format("%03d", ammoAmount);
						for ( int i; i < ammoString.length(); i++ )
						{
							TSP_DrawString(fnt_ammoSmall, ammoString.mid(i, 1), (HUDRIGHT_X+128 + 10*i, HUDRIGHT_Y+83), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW, a1.amount < maxClip ? CLR_AMMO_LOW : CLR_AMMO_NORMAL,
								alpha: (ammoAmount == 0 || (""..ammoAmount).length() < 3-i) ? 0.25 : 1.0);
						}
					}
					
					let crossTex = TexMan.CheckForTexture(wepon.tspw_crosshair);
					
					screen.DrawTexture(crossTex, false, screen.GetWidth()/2, screen.GetHeight()/2,
						DTA_CenterOffset, true);
				}
			}
			
			
			// EASY AUTO ACTIVATED
			/*if(CVar.GetCVar("tsp_easyauto", players[0]).GetBool()) //Auto-Assist Enabled
			{
			DrawString(mTinyFont, "\cfAuto", (HUDRIGHT_X+109, HUDRIGHT_Y+58), DI_TEXT_ALIGN_CENTER, Font.CR_GOLD);
			}*/
			
			//================
			// WEAPON PIP
			//================
			
			// MEL
			if(CPlayer.mo is "TSP_MelPlayer")
			{
				DrawImage("MEL_WPIE", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
				for(int i = 0;i<7;i++)
				{
					if(CPlayer.HasWeaponsInSlot(i+1))
					{
						DrawImage("MEL_WPI"..i, (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
					}
				}
				
				if(CPlayer.mo.FindInventory("MelChargeStyle"))
				{
					DrawImage("MEL_WPIX", (HUDRIGHT_X, HUDRIGHT_Y), DI_ITEM_OFFSETS);
				}
			}
			
			//================
			// Weapon Mod
			//================
			
				TSP_PlayerPawn_ZSCBase playerObj = TSP_PlayerPawn_ZSCBase(CPlayer.mo);
				if(playerObj.pp_notificationTime>0)
				{
					Centering = true;
					DrawString(mSmallFont, playerObj.pp_notification, (0,-36), DI_TEXT_ALIGN_CENTER|DI_NOSHADOW|DI_SCREEN_CENTER_BOTTOM, Font.CR_GREEN);
					Centering = false;
				}
			
			//================
			// Pickup Log
			//================
			
				for(int i = 0;i<playerObj.tsp_logs.Size();i++)
				{
					let thisone  = playerObj.tsp_logs[i];
					int yoff = 0;
					double alpher = 1.0;
					if(thisone.timeleft>138)
					{
						yoff = thisone.timeleft-138; //thunk
					}
					else if (thisone.timeleft<12)
					{
						alpher = thisone.timeleft/12.; //whoosh
					}
					DrawString(mTinyFont, String.Format(thisone.whatugot,thisone.howmany), (HUDLEFT_X+67, HUDLEFT_Y+15-(8*i)+yoff), DI_TEXT_ALIGN_LEFT, Font.CR_UNTRANSLATED, alpher);
				}
			
			//================
			// Notification Log
			//================
			
				for(int i = 0;i<tsp_notifs.Size();i++)
				{
					let thisone  = tsp_notifs[i];
					double alpher = 1.0;
					if(thisone.time>136)
					{
						alpher = (140.-thisone.time)/(140.-136.); //thunk
					}
					else if (thisone.time<35)
					{
						alpher = thisone.time/35.; //whoosh
					}
					DrawString(mSmallFont, thisone.message, (0, 4+9*i), DI_TEXT_ALIGN_CENTER|DI_SCREEN_HCENTER, Font.CR_UNTRANSLATED, alpher);
				}
			
			//================
			// Debug Shit
			//================
			
				/*if(isDebug)
				{
					string targetName = "N/A";
					if(playerObj.theZoop!=null)
					{
						targetName = playerobj.theZoop.GetClassName();
					}
					DrawString(mSmallFont, "\ccTarget: \c-"..targetName, (-2, 2), DI_TEXT_ALIGN_RIGHT|DI_NOSHADOW, Font.FindFontColor("TSPGreen"));
				}*/
				
				if(playerobj.pp_nextModSwitch != null)
				{
					bool foundIt;
					int slot; int alsoSlot; int index;
					int iconWidth = 32;
					[foundIt, slot] = CPlayer.weapons.LocateWeapon(CPlayer.ReadyWeapon.GetClass());
					[foundIt, alsoSlot, index] = CPlayer.weapons.LocateWeapon(playerobj.pp_nextModSwitch.GetClass());
					int slotMax = CPlayer.weapons.SlotSize(slot);
					for(int i = 0;i<slotMax;i++)
					{
						//let fucker = GetDefaultByType(CPlayer.weapons.GetWeapon(slot,i));
						bool hasIt = playerobj.pp_haveTheseWeapons.Find(CPlayer.weapons.GetWeapon(slot,i))!=playerobj.pp_haveTheseWeapons.Size();
						DrawImage("graphics/HUD3.0/weapons/weapon_mod_ph.png", (( ((-iconWidth*slotMax)/2)+iconWidth*i)+iconWidth/2 , -58), DI_SCREEN_CENTER_BOTTOM|DI_ITEM_CENTER, hasIt ? 1.0 : 0.5, (-1,-1), index==i ? (0.75,0.75) : (0.5,0.5));
							//DTA_DestWidth, 32, DTA_DestHeight, 32, DTA_TranslationIndex, hasIt ? -1 : Translation.GetID('TSP_Shop_Darken'));
					}
					DrawString(mSmallFont, "\cj[\cd"..playerobj.pp_nextModSwitch.tspw_altname.."\cj]", (0, -32), DI_TEXT_ALIGN_CENTER|DI_SCREEN_CENTER_BOTTOM, Font.CR_UNTRANSLATED, 1.0);
				}
		}
	}
	
	void TSP_DrawString (HUDFont doFont, String string, Vector2 pos, int flags = 0, Color color = CLR_WHITE, double alpha = 1.0, int wrapwidth = -1, int linespacing = 4, Vector2 scale = (1,1), int pt = 0, ERenderStyle style = STYLE_Translucent)
	{
		int xOff = 0;
		if ( flags & DI_TEXT_ALIGN_RIGHT )
		{
			xOff = -(doFont.mFont.StringWidth(string));
		}
		screen.DrawText(doFont.mFont, Font.CR_UNTRANSLATED, drawOffset.x+pos.x+xOff, drawOffset.y+pos.y, string,
			DTA_Alpha, alpha, DTA_COLOR, color,
			DTA_HUDRules, BaseStatusBar.HUD_NORMAL);
		/*let tex = TexMan.CheckForTexture("HUD_LBAS");
		screen.DrawTexture(tex, false, HUDLEFT_X, HUDLEFT_Y,
			DTA_HUDRules, BaseStatusBar.HUD_NORMAL);*/
	}
}
