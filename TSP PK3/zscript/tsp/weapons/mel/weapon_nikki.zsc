Class MelPistol : TSP_Mel_BaseWeapon
{
	string fireSound;
	
	property FireSound : fireSound;
	
	Default
	{
		Inventory.PickupMessage "$MELPISTOLPICKUP";
		Obituary "%o learned why %k is the king of the iron fist.";
		Weapon.Kickback 110;
		Weapon.BobStyle "Alpha";
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 2.2;
		Weapon.BobRangeY 0.5;
		Weapon.BobRangeX 0.7;
		Weapon.AmmoType "TSPPistolAmmo";
		Weapon.AmmoGive 0;//0;
		Weapon.AmmoUse 1;
		Inventory.Icon "121PA0";

			TSP_Weapon_Core.AmmoMult 1.35, 1.7, 2.0;
			TSP_Weapon_Core.ClipSize 15; //Initial starting size of clip
			TSP_Weapon_Core.FirstPickupLine "mel/nikkipickup"; //AAAAGH
			+TSP_Weapon_Core.MODFIREMODE
			+TSP_Weapon_Core.MODFIREZOOMS
			+TSP_Weapon_Core.HOLDDOWNFORMOD
			TSP_Weapon_Core.HudColor "#0FA4F1";

		Tag "$MELPISTOLTAG";
		+Weapon.NOAUTOFIRE
		+Weapon.AMMO_OPTIONAL
		+NOEXTREMEDEATH
		+WEAPON.NoAlert
		+WEAPON.CHEATNOTWEAPON
		
		MelPistol.FireSound "nikki/default/fire";
		
		TSP_Weapon_Core.Portrait "graphics/hud/weapons/nikki_unmodded.png";
		
		TSP_Weapon_Core.SpriteNameBase "121";
	}
	
	action void A_TSP_MelPistol_Fire(int xoff = 4)
	{
		A_AlertMonsters();
		A_StartSound(invoker.firesound, 6);
		A_TSP_FireBullets(3,2,-1,8,"TSP_BulletPuff",1, Spawnofs_xy: xoff);
		A_TSP_TakeClip(1);
	}
	
	States
	{
		Precache:
			121G A 0;
			121R A 0;
			121I A 0;
			121D A 0;
			Stop;
		Spawn:
			121P A -1;
			Stop;
			
		Select:
			TNT1 A 0
			{
				A_TakeInventory("MeleeSelected", 1);
				A_TakeInventory("WeaponSelectMemory", 999);
				A_GiveInventory("WeaponSelectMemory", 121);
				A_StartSound("nikki/draw", 5);
				A_WeaponOffset(0,32);
				A_TSP_SetSpriteFrame(PSP_WEAPON, "D");
				invoker.anim_drawGun.PlayAnim();
			}
			"####" A 3;
			"####" BC 2;
			"####" D 3;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" A 1 A_Raise;
			Loop;
		ModOverlay:
			122S A 15;
			122S A 3 A_StartSound("nikki/upgrade/spitfire", CHAN_5);
			122S B 3;
			122S C 6;
			Stop;
		
		ModOn:
			"----" A 0
			{
				if ( invoker.GetClassName() == "MelPistol" )
				{
					return resolveState("Select");
				}
				else
				{
					A_Overlay(PSP_TSP_RELOADHAND, "ModOverlay");
					A_WeaponOffset(0,32);
					invoker.anim_modSwap_gun.PlayAnim();
					invoker.anim_modSwap_mod.PlayAnim();
				}
				return resolveState(null);
			}
			121S A 21;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "R");
			122R A 0;
			"####" C 7;
			"####" A 2;
			122G A 0;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" A 5;
			"----" A 0 A_Raise;
			Goto Ready;
			/*122S BBBBB 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			/*122S A 1
			{
				A_StartSound("nikki/upgrade/spitfire", 5);
				A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			}
			122S AAAA 1;
			122S AAAAA 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);*/
			122G A 1 A_Raise;
			Goto Ready;
		Deselect:
			"----" A 0
			{
				A_TSP_DeselectLogic();
				A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
				A_TakeInventory("TSPIdle", 99999);
				A_TakeInventory("StyleComboToken", 99);
				A_TakeInventory("ComboCooldown", 99);
				A_StartSound("nikki/holster", 5);
			}
			"####" A 1 A_WeaponOffset(0, 32+1, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+2, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+4, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+8, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+16, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+32, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+64, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+128, WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0, 32+256, WOF_INTERPOLATE);
			TNT1 A 0 A_Lower;
			Wait;
		Ready:
			TNT1 A 0
			{
				if ( CountInv("MeleePressed") ) return resolveState("SPMelee");
				if ( CountInv("TraitPressed") ) return resolveState("SPTrait");
				if ( CountInv("TSPIdle") >= 1575 ) return resolveState("Idle");
				if ( CountInv("TauntPressed") ) return resolveState("Taunt");
				
				A_GiveInventory("TSPIdle", 1);
				A_SetCrosshair(0);
				A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
				return resolveState(null);
			}
			"####" A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"####" A 0 A_TSP_GotoReadyState;
			Loop;
			
		Casing:
			"----" A 0 A_Jump(256,random(0,8));
			120C AAAABBBBCCCCDDDDEEEEFFFFGGGG 1;
			TNT1 A -1;
			Stop;
			
		Flash:
			"----" A 0
			{
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
				A_OverlayScale(PSP_FLASH, 1.0);
			}
			121F A 0 A_Jump(256,"Flash1","Flash2","Flash3","Flash4");
			Loop;
			Flash1:
				121F A 1 BRIGHT;
				Stop;
			Flash2:
				121F B 1 BRIGHT;
				Stop;
			Flash3:
				121F C 1 BRIGHT;
				Stop;
			Flash4:
				121F D 1 BRIGHT;
				Stop;
		AltFlash:
			"----" A 0
			{
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
				A_OverlayScale(PSP_FLASH, 0.9);
			}
			121F A 0 A_Jump(256,"AltFlash1","AltFlash2","AltFlash3","AltFlash4");
			Loop;
			AltFlash1:
				121F E 1 BRIGHT;
				Stop;
			AltFlash2:
				121F F 1 BRIGHT;
				Stop;
			AltFlash3:
				121F G 1 BRIGHT;
				Stop;
			AltFlash4:
				121F H 1 BRIGHT;
				Stop;

	// Idle Animations   
		Taunt:
			"----" A 0 A_AlertMonsters;
		Idle:
			"----" A 0 A_TakeInventory("TSPIdle", 99999);
		Idle1:
			TNT1 A 0
			{
				A_StartSound("meltemp", CHAN_VOICE);
				A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
				invoker.anim_spinGun.PlayAnim();
			}
			"####" A 3 A_WeaponReady;
			"####" A 0 A_StartSound("nikki/idle01", 5);
			"####" A 3 A_WeaponReady;
			
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "I");
			"####" FBC 2 A_WeaponReady;
			"####" DEF 1 A_WeaponReady;
			"####" BCDEFBCDEFBCDEF 1 A_WeaponReady;
			"####" BC 1 A_WeaponReady;
			
			//"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "I");
			"####" AA 1 A_WeaponReady;
			//"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "I");
			"####" A 17 A_WeaponReady;
			Goto Ready;

		Fire:
			"----" A 0 A_TakeInventory("TSPIdle", 99999);
			"----" A 0 A_TSP_JumpIfClipEmpty("Empty");
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"----" A 1;
			"####" B 1 BRIGHT
			{
				invoker.anim_spinGun.StopAnim();
				A_TSP_MelPistol_Fire(4);
				A_Light2();
				TSP_PlayerPawn_ZSCBase(invoker.owner).TSP_SpawnCasing(ResolveState("Casing"), (212, 136), frandom(-5,-25)-10, 52);
				A_GunFlash("Flash");
			}

			"####" C 1
			{
				A_Light1();
				A_TSP_WeaponRecoilDir(random(30,40), 32+frandom(0,2), 1);
			}
			"####" C 1
			{
				A_Light0();
				A_TSP_WeaponRecoilDir(random(50,60), 12+frandom(0,2), 1);
			}
			"####" C 1 A_TSP_WeaponReady(WRF_NOBOB);
			"####" D 3 A_TSP_WeaponReady(WRF_NOBOB);
			"####" E 3 A_TSP_WeaponReady(WRF_NOBOB);
			"####" AA 1 A_TSP_WeaponReady(WRF_NOBOB);
			"####" A 0 A_TSP_GotoReadyState;
			Goto Ready;
		
		ModReady:
			TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
			TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait");
			121A A 0 A_SetCrosshair(99);
			"----" A 0 A_WeaponOffset(-44.5, 26, WOF_INTERPOLATE);
			121A A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"----" A 0 A_TSP_GotoReadyState;
			Loop;
		ModFire:
			"----" A 0 A_TakeInventory("TSPIdle", 99999);
			"----" A 0 A_TSP_JumpIfClipEmpty("Empty");
			"----" A 0 A_AlertMonsters;
			"----" A 1;
			TNT1 A 0 A_Light2;
			121A A 1 BRIGHT
			{
				A_TSP_MelPistol_Fire(0);
				TSP_PlayerPawn_ZSCBase(invoker.owner).TSP_SpawnCasing(ResolveState("Casing"), (160, 134), frandom(-5,-25)-20, 52*1.25);
				A_GunFlash("AltFlash");
			}
			121A B 1 
			{
				A_Light1();
				A_TSP_WeaponRecoilDir(90+random(-2,2), 10+frandom(0,2), 1);
			}
			121A C 1
			{
				A_Light0();
				A_TSP_WeaponRecoilDir(90+random(-2,2), 2+frandom(0,2), 1);
			}
			121A C 1 A_TSP_WeaponReady(WRF_NOBOB);
			121A A 3 A_TSP_WeaponReady(WRF_NOBOB);
			121A A 3 A_TSP_WeaponReady(WRF_NOBOB);
			121A AA 1 A_TSP_WeaponReady(WRF_NOBOB);
			"----" A 0 A_TSP_GotoReadyState;
			Goto Ready;
		
		Empty:
			"----" A 5 A_StartSound("nikki/empty", 5);
			"----" A 0 A_TSP_CheckReload();
			Goto Ready;
		Reload:
			"----" A 0 A_WeaponOffset(0, 32);
			"----" A 0 A_TSP_JumpIfModInactive("ReloadFinish");
			"----" A 0
			{
				invoker.tspw_altmode = false;
				invoker.anim_spinGun.StopAnim();
				A_OverlayScale(PSP_WEAPON, 1.0);
			}
			"----" A 0 A_TSP_ZoomFactor(1.0);
			//121S B 1;
			//121S A 1;
			"----" A 0 A_TSP_WeaponRecoilDir(random(1,5), 4, 1);
		ReloadFinish:
			"----" A 0
			{
				A_Overlay(PSP_TSP_RELOADHAND, "ReloadHand");
				invoker.anim_reloadStart_gun.PlayAnim();
				invoker.anim_reloadStart_hand.PlayAnim();
			}
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" A 6;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "R");
			"####" A 2;
			//"----" A 0 A_Overlay(PSP_TSP_UNDERGUN, "ReloadUnderGun");
			"####" C 4;
			"####" C 1 A_StartSound("nikki/eject", 6);
			"----" A 0 A_Overlay(PSP_TSP_UNDERGUN, "ReloadUnderGun");
			
			"####" D 22;
			"####" C 28;
			"----" A 0 A_Overlay(PSP_TSP_UNDERGUN, null);
			"####" A 2;//"####" E 9;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" AAAAAAAAAAA 1 A_TSP_WeaponReady(WRF_NOBOB);
			/*121R JKLLMNO 1;
			121R P 20 A_TSP_ReloadSpeed(20);
			//121R PPPPPPPPPPPPPPPPPPPPPP 1//22
			121R QRS 1;
			TNT1 A 0 A_StartSound("nikki/load", 5);
			121R TUUUVWXYZ 1;
			TNT1 A 0 A_TSP_ReloadAmmo;
			121R Z 1 A_StartSound("nikki/tap", 6);
			121S AAABCD 1;
			121G A 3;*/
			Goto Ready;
			
		ReloadUnderGun:
			"----" A 0 A_TSP_SetSpriteFrame(PSP_TSP_UNDERGUN, "R");
			"####" B 1;
			Loop;
			
		ReloadHand:
			TNT1 A 13;
			120H D 10;
			120H A 12;
			120H B 10
			{
				A_StartSound("nikki/load", 6);
				A_TSP_ReloadAmmo();
			}
			"----" A 0 A_Overlay(PSP_TSP_UPPERHAND, "ReloadUpperHand");
			120H C 6;
			120H C 3 A_StartSound("nikki/tap", 6);
			120H C 10;
			Stop;
			
		ReloadUpperHand:
			120H C 15;
			Stop;
			
		Switch:
			"----" A 0
			{
				A_StartSound("nikki/default/aim01", 5);
				A_TSP_ZoomFactor(0.75);
				A_OverlayScale(PSP_WEAPON, 0.9);
				invoker.anim_spitFire_on.PlayAnim();
			}
			"----" A 0 A_WeaponOffset(-32, 32, WOF_INTERPOLATE);
			121G AA 1;
			//121A AA 1 A_WeaponOffset(-44.5, 26, WOF_INTERPOLATE);
			"----" A 0 A_TSP_WeaponRecoilDir(180+random(1,5), 4, 1);
			//131S B 1 A_WeaponOffset(-12,32,WOF_INTERPOLATE)
			Goto ModReady;
		ModSwitch:
			"----" A 0
			{
				A_StartSound("nikki/default/aim02", 5);
				A_TSP_ZoomFactor(1.0);
				A_OverlayScale(PSP_WEAPON, 1.0);
				invoker.anim_spitFire_on.PlayAnim();
			}
			"----" A 0 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
			121G A 1;
			121G A 1;
			"----" A 0 A_TSP_WeaponRecoilDir(random(1,5), 4, 1);
			Goto Ready;
	  }
}