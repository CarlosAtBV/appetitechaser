Class MelPistol : TSP_Mel_BaseWeapon
{
	string fireSound;
	
	property FireSound : fireSound;
	
	const PSP_TSP_UNDERGUN = -10;
	const PSP_TSP_RELOADHAND = -5;
	
	Default
	{
		Inventory.PickupMessage "$MELPISTOLPICKUP";
		Obituary "%o learned why %k is the king of the iron fist.";
		Weapon.Kickback 110;
		Weapon.BobStyle "Alpha";
		Weapon.BobStyle "InverseSmooth";
		Weapon.BobSpeed 2.2;
		Weapon.BobRangeY 0.5;
		Weapon.BobRangeX 0.7;
		Weapon.AmmoType "TSPPistolAmmo";
		Weapon.AmmoGive 0;//0;
		Weapon.AmmoUse 1;
		Inventory.Icon "121PA0";

			TSP_Weapon_Core.AmmoMult 1.35, 1.7, 2.0;
			TSP_Weapon_Core.ClipSize 15; //Initial starting size of clip
			TSP_Weapon_Core.FirstPickupLine "mel/nikkipickup"; //AAAAGH
			+TSP_Weapon_Core.MODFIREMODE
			+TSP_Weapon_Core.MODFIREZOOMS
			+TSP_Weapon_Core.HOLDDOWNFORMOD

		Tag "$MELPISTOLTAG";
		+Weapon.NOAUTOFIRE
		+Weapon.AMMO_OPTIONAL
		+NOEXTREMEDEATH
		+WEAPON.NoAlert
		+WEAPON.CHEATNOTWEAPON
		
		MelPistol.FireSound "nikki/fire";
		
		TSP_Weapon_Core.SpriteNameBase "121";
	}
	
	action void A_TSP_MelPistol_Fire(int xoff = 4)
	{
		A_AlertMonsters();
		A_PlaySound(invoker.firesound, 6);
		A_TSP_FireBullets(3,2,-1,8,"TSP_BulletPuff",1, Spawnofs_xy: xoff);
		A_TSP_TakeClip(1);
	}
	
	States
	{
		Spawn:
			121P A -1;
			Stop;
		Select:
			TNT1 A 0 A_TakeInventory("MeleeSelected", 1);
			TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999);
			TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 121);
			TNT1 A 0 A_PlaySound("nikki/draw", 5);
			TNT1 A 0 A_WeaponOffset(0,32);
			"----" A 0 A_TSP_AllowZoom(true);
			121U ABCDEFGHIJKLM 1;
			121G A 1;
			121G A 1 A_Raise;
			Loop;
		ModOff:
			TNT1 A 0 A_WeaponOffset(0,32);
			121G A 1;
			122V E 1;
			122U ABCDDEEFFGGHH 1;
			122U III 1 A_PlaySound("nikki/upgraderemove", 6);
			122U JKKLLLMNOP 1;
			122U Q 0;//122U Q 10
			"----" A 0 A_Jump(256,"WaitForModSwitch"); //has 2 be a jump :pensive:
			Stop;
		ModOn:
			TNT1 A 0 A_WeaponOffset(0,32);
			122U PONML 1;
			122U K 1 A_PlaySound("nikki/upgradespitfire", 5);
			122U JIIIIHH 1;
			122U GFEDDD 1;
			122U CB 1;
			122G A 1 A_Raise;
			Goto Ready;
		Deselect:
			TNT1 A 0 A_TakeInventory("TSPIdle", 99999);
			TNT1 A 0 A_TakeInventory("StyleComboToken", 99);
			TNT1 A 0 A_TakeInventory("ComboCooldown", 99);
			TNT1 A 0 A_PlaySound("nikki/holster", 5);
			121G A 1;
			121U IHGFEDC 1;
			TNT1 A 5;
		DeselectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower;
			TNT1 A 1 A_Lower;
			Loop;
		Ready:
			TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
			TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait");
			TNT1 A 0 A_JumpIfInventory("TSPIdle", 1575, "Idle");
			TNT1 A 0 A_JumpIfInventory("TauntPressed",1,"Taunt");
			TNT1 A 0 A_GiveInventory("TSPIdle", 1);
			TNT1 A 0 A_SetCrosshair(0);
			TNT1 A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"####" A 0 A_TSP_GotoReadyState;
			Loop;
			
		Casing:
			"----" A 0 A_Jump(256,random(0,8));
			120C AAAABBBBCCCCDDDDEEEEFFFFGGGG 1;
			TNT1 A -1;
			Stop;
			
		Flash:
			"----" A 0
			{
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
			121F A 0 A_Jump(256,"Flash1","Flash2","Flash3","Flash4");
			Loop;
			Flash1:
				121F A 1 BRIGHT;
				Stop;
			Flash2:
				121F B 1 BRIGHT;
				Stop;
			Flash3:
				121F C 1 BRIGHT;
				Stop;
			Flash4:
				121F D 1 BRIGHT;
				Stop;
		AltFlash:
			"----" A 0
			{
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
			121F A 0 A_Jump(256,"AltFlash1","AltFlash2","AltFlash3","AltFlash4");
			Loop;
			AltFlash1:
				121F E 1 BRIGHT;
				Stop;
			AltFlash2:
				121F F 1 BRIGHT;
				Stop;
			AltFlash3:
				121F G 1 BRIGHT;
				Stop;
			AltFlash4:
				121F H 1 BRIGHT;
				Stop;

	// Idle Animations   
		Taunt:
			TNT1 A 0 A_AlertMonsters;
		Idle:
			TNT1 A 0 A_TakeInventory("TSPIdle", 99999);
		Idle1:
			TNT1 A 0 A_PlaySound("meltemp", CHAN_VOICE);
			TNT1 A 0 A_PlaySound("nikki/idle01", 5);
			121G A 2 A_WeaponReady;
			121U MLKK 1 A_WeaponReady;
			121T AAABBCDEFGGAABCDEFGGHIJKKKKKKKKKL 1 A_WeaponReady;
			121U KLM 1 A_WeaponReady;
			121G A 2 A_WeaponReady;
			Goto Ready;

		Fire:
			"----" A 0 A_TakeInventory("TSPIdle", 99999);
			"----" A 0 A_TSP_JumpIfClipEmpty("Empty");
			"----" A 1;
			"####" B 1 BRIGHT
			{
				A_TSP_MelPistol_Fire(4);
				A_Light2();
				TSP_PlayerPawn_ZSCBase(invoker.owner).TSP_SpawnCasing(ResolveState("Casing"), (212, 136), frandom(-5,-25)-10, 52);
				A_GunFlash("Flash");
			}
			"####" C 1 A_TSP_WeaponRecoilDir(random(30,40), 32+frandom(0,2), 1);
			"####" C 1 A_TSP_WeaponRecoilDir(random(50,60), 12+frandom(0,2), 1);
			"####" C 1 A_TSP_WeaponReady(WRF_NOBOB);
			"####" D 3 A_TSP_WeaponReady(WRF_NOBOB);
			"####" E 3 A_TSP_WeaponReady(WRF_NOBOB);
			"####" AA 1 A_TSP_WeaponReady(WRF_NOBOB);
			"####" A 0 A_TSP_GotoReadyState;
			Goto Ready;
		
		ModReady:
			TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
			TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait");
			121I A 0 A_SetCrosshair(99);
			121I A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"----" A 0 A_TSP_GotoReadyState;
			Loop;
		ModFire:
			"----" A 0 A_TakeInventory("TSPIdle", 99999);
			"----" A 0 A_TSP_JumpIfClipEmpty("Empty");
			"----" A 0 A_AlertMonsters;
			"----" A 1;
			TNT1 A 0 A_Light2;
			121A A 1 BRIGHT
			{
				A_TSP_MelPistol_Fire(0);
				TSP_PlayerPawn_ZSCBase(invoker.owner).TSP_SpawnCasing(ResolveState("Casing"), (150, 134), frandom(-5,-25)-20, 52*1.25);
				A_GunFlash("AltFlash");
			}
			121A B 1 A_TSP_WeaponRecoilDir(90+random(-2,2), 20+frandom(0,2), 1);
			121A B 1 A_TSP_WeaponRecoilDir(90+random(-2,2), 8+frandom(0,2), 1);
			121A B 1 A_TSP_WeaponReady(WRF_NOBOB);
			121A C 3 A_TSP_WeaponReady(WRF_NOBOB);
			121A D 3 A_TSP_WeaponReady(WRF_NOBOB);
			121I AA 1 A_TSP_WeaponReady(WRF_NOBOB);
			"----" A 0 A_TSP_GotoReadyState;
			Goto Ready;
		
		Empty:
			"----" A 5 A_PlaySound("nikki/empty", 5);
			"----" A 0 A_TSP_CheckReload();
			Goto Ready;
		Reload:
			"----" A 0 A_TSP_JumpIfModInactive("ReloadFinish");
			"----" A 0
			{
				invoker.tspw_altmode = false;
			}
			121S B 1;
			121S A 1;
			"----" A 0 A_TSP_WeaponRecoilDir(random(1,5), 4, 1);
		ReloadFinish:
			"----" A 0 A_Overlay(PSP_TSP_RELOADHAND, "ReloadHand");
			"----" A 0 A_Overlay(70, "Overlay_Reload_Weapon");
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" A 4;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "R");
			"####" A 3;
			"----" A 0 A_Overlay(PSP_TSP_UNDERGUN, "ReloadUnderGun");
			"####" C 7;
			"####" B 28;
			"####" E 9;
			"####" C 7;
			"----" A 0 A_Overlay(PSP_TSP_UNDERGUN, null);
			"####" A 2;
			"----" A 0 A_TSP_SetSpriteFrame(PSP_WEAPON, "G");
			"####" AAAAAAAAAAA 1 A_TSP_WeaponReady(WRF_NOBOB);
			/*121R JKLLMNO 1;
			121R P 20 A_TSP_ReloadSpeed(20);
			//121R PPPPPPPPPPPPPPPPPPPPPP 1//22
			121R QRS 1;
			TNT1 A 0 A_PlaySound("nikki/reloadin1", 5);
			121R TUUUVWXYZ 1;
			TNT1 A 0 A_TSP_ReloadAmmo;
			121R Z 1 A_PlaySound("nikki/reloadin2", 6);
			121S AAABCD 1;
			121G A 3;*/
			Goto Ready;
			
		ReloadUnderGun:
			"----" A 0 A_TSP_SetSpriteFrame(PSP_TSP_UNDERGUN, "R");
			"####" D 1;
			Loop;
			
		ReloadHand:
			"----" A 0 A_Overlay(-11, "Overlay_Reload_Hand");
			121H A 14;
			121H C 4;
			121H C 10 A_PlaySound("nikki/reloadout", 6);
			121H C 4
			{
				A_TSP_WeaponRecoilDir(0, 1, 1);
				A_TSP_ReloadMult();
			}
			121H C 10 A_PlaySound("nikki/reloadin1", 6);
			121H A 2;
			121H B 7;
			121H A 15
			{
				A_PlaySound("nikki/reloadin2", 6);
				A_TSP_ReloadAmmo();
			}
			Stop;
			
		Switch:
			121S A 1;
			121S B 1;
			"----" A 0 A_TSP_WeaponRecoilDir(180+random(1,5), 4, 1);
			//131S B 1 A_WeaponOffset(-12,32,WOF_INTERPOLATE)
			Goto ModReady;
		ModSwitch:
			121S B 1;
			121S A 1;
			"----" A 0 A_TSP_WeaponRecoilDir(random(1,5), 4, 1);
			Goto Ready;
	  }
}