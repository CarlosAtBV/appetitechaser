Class MelPistolA : MelPistol
{
	Default
	{
		TSP_Weapon_Core.ModTag "Spitfire" ;
		+WEAPON.CHEATNOTWEAPON
		//-TSP_Weapon_Core.HOLDDOWNFORMOD
		
		TSP_Weapon_Core.Portrait "graphics/hud/weapons/nikki_spitfire.png";
		TSP_Weapon_Core.SpriteNameBase "122";
		
		MelPistol.FireSound "nikki/spitfire/fire";
	}
	
	States
	{
		Precache:
			122G A 0;
			122R A 0;
			122D A 0;
			122I A 0;
			Stop;
		Switch:
			"----" A 1
			{
				A_StartSound("nikki/spitfire/aim01", 5);
				A_TSP_ZoomFactor(0.9);
				invoker.anim_spitFire_on.PlayAnim();
			}
			"----" A 0 A_TSP_WeaponRecoilDir(180+random(1,5), 2, 1);
			//131S B 1 A_WeaponOffset(-12,32,WOF_INTERPOLATE)
			Goto ModReady;
		ModSwitch:
			"----" A 1
			{
				A_StartSound("nikki/spitfire/aim02", 5);
				A_TSP_ZoomFactor(1.0);
				invoker.anim_spitFire_on.PlayAnim();
			}
			"----" A 0 A_TSP_WeaponRecoilDir(random(1,5), 2, 1);
			Goto Ready;
		
		Flash:
			"----" A 0
			{
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
			121F A 0 A_Jump(256,"Flash1","Flash2","Flash3","Flash4");
			Loop;
			Flash1:
				122F A 1 BRIGHT;
				Stop;
			Flash2:
				122F B 1 BRIGHT;
				Stop;
			Flash3:
				122F C 1 BRIGHT;
				Stop;
			Flash4:
				122F D 1 BRIGHT;
				Stop;
		AltFlash:
			"----" A 0
			{
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
			122F A 0 A_Jump(256,"AltFlash1","AltFlash2","AltFlash3","AltFlash4");
			Loop;
			AltFlash1:
				122F E 1 BRIGHT;
				Stop;
			AltFlash2:
				122F F 1 BRIGHT;
				Stop;
			AltFlash3:
				122F G 1 BRIGHT;
				Stop;
			AltFlash4:
				122F H 1 BRIGHT;
				Stop;
		
		ModReady:
			TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
			TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait");
			122A A 0 A_SetCrosshair(99);
			122A A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"----" A 0 A_TSP_GotoReadyState;
			Loop;
		ModFire:
			"----" A 0 A_TakeInventory("TSPIdle", 99999);
			"----" A 0 A_TSP_JumpIfClipEmpty("Empty");
			"----" A 1;
			TNT1 A 0 A_Light2;
			122A B 1 BRIGHT
			{
				A_StartSound(invoker.firesound, CHAN_6);
				A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1, Spawnofs_xy: 0);
				A_TSP_WeaponRecoilDir(90+frandom(-4,4), 4+frandom(-1,1), 1);
				TSP_PlayerPawn_ZSCBase(invoker.owner).TSP_SpawnCasing(ResolveState("Casing"), (202, 136), frandom(-5,-25)-10, 52);
				A_SetAngle(angle + (frandom(-0.3,0.3)));
				A_SetPitch(pitch - 0.8);
				A_GunFlash("AltFlash");
				A_TSP_TakeClip(1);
			}
			122A A 1;
			"----" A 0 A_Refire("ModFire");
			"----" A 0 A_TSP_GotoReadyState;
			Goto Ready;
	}
}