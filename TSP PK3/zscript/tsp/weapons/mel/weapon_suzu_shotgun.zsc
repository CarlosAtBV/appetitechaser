Class MelSMGA : MelSMG
{
	Default
	{
		TSP_Weapon_Core.ModTag "Shotgun" ;
		+WEAPON.CHEATNOTWEAPON
		Tag "$MELSMGATAG";
		Weapon.AmmoUse2 2;
		-TSP_Weapon_Core.HOLDDOWNFORMOD
		-TSP_Weapon_Core.MODFIREZOOMS
		TSP_Weapon_Core.SpriteNameBase "132";
		TSP_Weapon_Core.Portrait "graphics/hud/weapons/suzu_shotgun.png";
	}
	
	States
	{
		Spawn:
			131P A -1;
			Stop;
		
		Deselect:
			"----" A 0 A_TSP_JumpIfModInactive("DeselectFinal");
			TNT1 A 0
			{
				A_StartSound("suzuka/shotgun/off", 5);
				A_TSP_WeaponRecoilDir(random(80,90),frandom(1,2));
			}
			132A CBA 1;
			132G A 0;
			Goto DeselectFinal;
			
		Ready:
			TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
			TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait");
			132G A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"####" A 0 A_TSP_GotoReadyState;
			Loop;
		ModReady:
			TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
			TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait");
			132A D 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
			"####" A 0 A_TSP_GotoReadyState;
			Loop;
		
		Fire:
			"####" A 0 A_TSP_JumpIfClipEmpty("Empty");
			"####" A 0 A_StartSound("suzuka/shotgun/primaryfire", CHAN_WEAPON|CHANF_OVERLAP);
			"----" A 0 A_Jump(256, "FireEnd");
			Stop;
			
		Switch:
			TNT1 A 0
			{
				A_StartSound("suzuka/shotgun/on", 5);
				A_TSP_WeaponRecoilDir(random(80,90),frandom(1,2));
			}
			132A ABC 1;
			Goto ModReady;
		ModSwitch:
			TNT1 A 0
			{
				A_StartSound("suzuka/shotgun/off", 5);
				A_TSP_WeaponRecoilDir(random(80,90),frandom(1,2));
			}
			132A CBA 1;
			Goto Ready;
		
		ModFire:
			132A D 0 A_TSP_JumpIfClipLower(8,"Empty");
			TNT1 A 0
			{
				A_GunFlash("ModFlash");
				A_StartSound("suzuka/shotgun/altfire", 6);
				A_AlertMonsters();
				A_Quake(2,3,0,5,"");
				A_TSP_FireBullets(6,2,8,7,"TSP_BulletPuff",1,0,"TSP_HitscanTracer", frandom(-3,-1), tracerOffset: (frandom(3,5), -3));
				A_TSP_TakeClip(8);
				A_SetPitch(pitch - 2.5);
				A_SetAngle(angle + (frandom(-0.5,0.5)));
			}
			132A E 1 BRIGHT
			{
				A_Light2();
				A_TSP_WeaponRecoilDir(45,frandom(24,32));
			}
			132A E 1
			{
				A_Light1();
				A_TSP_WeaponRecoilDir(random(35,55),frandom(40,48),1);
			}
			132A E 4 A_Light0;
			132A F 2;
			132A D 3;
			132A DFE 1 A_TSP_WeaponRecoilDir(45,-2,1);
			//cocking anim
			"----" A 0
			{
				A_StartSound("suzuka/pump", 5);
				A_TSP_WeaponRecoilDir(random(35,55)-10,18,1);
			}
			132A EEEE 1 A_TSP_WeaponRecoilDir(35,6,1);
			132A EF 2;
			//ok done
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-5,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-3,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-4,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-7,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-10,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-11,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-4,0,0,0);
			TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-6,0,0,0);
			"----" A 0 A_TSP_GotoReadyState;
			Goto Ready;
		
		Reload:
			"----" A 0 A_TSP_JumpIfModInactive("ReloadFinish");
			"----" A 0
			{
				A_StartSound("suzuka/shotgun/off", 5);
				A_TSP_WeaponRecoilDir(random(80,90),frandom(1,2));
			}
			132A CBA 1;
			132G AAA 1;
			"----" A 0 A_Jump(256, "ReloadFinish");
			Stop;
			
		Flash1:
			132F A 1 BRIGHT;
			Stop;
		Flash2:
			132F B 1 BRIGHT;
			Stop;
		Flash3:
			132F C 1 BRIGHT;
			Stop;
		Flash4:
			132F D 1 BRIGHT;
			Stop;
		
		SpriteStore:
			132R A 0;
			132S A 0;
			132M A 0;
			Stop;
			
	}
}