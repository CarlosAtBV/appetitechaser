Class TSP_HitscanTracer : FastProjectile
{
	Default
	{
		Radius 1;
		Height 2;
		Speed 120;
		RenderStyle "Add";
		+BRIGHT
	}
	
	States
	{
		Spawn:
			POSS A -1;
			Stop;
	}
}


Class TSP_HitscanTracer_Enemy : TSP_HitscanTracer
{
	int tracerDamage;
	
	override int SpecialMissileHit (Actor victim)
	{
		if(victim!=target) victim.DamageMobj(self,target,tracerDamage,'Hitscan');
		return -1;
	}
	
	States
	{
		XDeath:
			TNT1 A 15 A_PlaySound("hit/flesh");
			Stop;
	}
}


Class TSP_BulletPuff : Actor
{
	Default
	{
		+PUFFONACTORS
		XScale 0.05;
		YScale 0.05;
		RenderStyle "Add";
		+ROLLSPRITE
		+BRIGHT
	}
	
	int timeLeft;
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		roll = random(0,360);
		scale *= frandom(0.8,1.2);
	}
	
	override void Tick()
	{
		scale.x += 0.2;
		scale.y += 0.2;
		timeLeft += 1;
		if(timeLeft>=3)
		{
			Destroy();
		}
	}
	
	States
	{
		Spawn:
			SFXA A -1;
			Stop;
		XDeath:
			TNT1 A 0 A_PlaySound("hit/flesh");
			Stop;
	}
}