/////////////////////////////////////////////////////
// Allows for both combo-able trait and melee attacks
//
// Because Mel and Robo Mel share some weapons, you
// might see some combined shenanigans in here!
/////////////////////////////////////////////////////

class TSP_MelRetro_BaseWeapon : TSP_Weapon_Core
{
	DEFAULT
	{
	+INVENTORY.UNTOSSABLE
	}
	States
	{
	Select:
		TNT1 A 1 A_Raise;
		Loop;
	Deselect:
		TNT1 A 1 A_Lower;
		Loop;
    ComboDeselect:
        TNT1 A 0 A_TakeInventory("ComboCoolDown", 99);
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower;
        TNT1 A 1 A_Lower;
        Loop;
	Ready:
	    TNT1 A 1;
		Loop;
	Fire:
		TNT1 A 1;
		Loop;
	SPTrait:
		TNT1 A 2 A_WeaponReady;
		Loop;
	TraitFire:
		"----" A 0 A_TSP_JumpIfInAir("DeadlyAxe");
		MLGF A 0 A_StartSound("*maggrapple", 6);
		MLGF ABCDEFGH 1;
		TNT1 A 0 A_SetBlend("39 99 0", 0.4, 15);
		MLGF I 1 A_TSP_Grapple;
		MLGF JKLM 1;
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1);
		TNT1 A 6;
		TNT1 AAAAAAAAAAAAA 1 A_WeaponReady;
		TNT1 A 0 A_Jump(256, "AfterMelee");
		TNT1 A 0;
		Goto AfterMelee;
	TraitAltFire:
		//"----" A 0 A_JumpIf(CountInv("TSP_Upgrade_MagSling")<1,"Ready");
		MLGF A 0 A_StartSound("*magdrive", 6);
		MLGF ABCDEFGH 1;
		TNT1 A 0 A_SetBlend("18 65 32", 0.4, 15);
		MLGF I 1
		{
			A_TSP_Melee(50,90,"BlankMeleePuff", 0,32,8);
			A_SpawnItemEx ("MagPushProjectile",20,0,18,cos(-pitch)*200,0,sin(-pitch)*200,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx ("MagPushProjectileReflective",0,0,18,cos(-pitch)*60,0,sin(-pitch)*60,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx ("MagPushProjectileReflective",0,15,18,cos(-pitch)*60,0,sin(-pitch)*60,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx ("MagPushPainProjectile",20,0,18,cos(-pitch)*200,0,sin(-pitch)*200,0,SXF_NOCHECKPOSITION);
			A_Quake(3,2,0,3,"");
		}
		MLGF HGFE 1 A_Quake(6,2,0,5,"");
		MLGF DCBA 1;
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1);
		TNT1 A 6;
		TNT1 AAAAAAAAAAAAA 1 A_WeaponReady;
		TNT1 A 0 A_Jump(256, "AfterMelee");
		Goto AfterMelee;
//DIVEKICK!
	DeadlyAxe:
		TNT1 A 6 Thrustthingz(0,40,0,0);
		TNT1 A 0 A_StartSound("strongmelee/miss", 5, CHAN_OVERLAP);
		1126 ABCDDEEEEE 1;
	    TNT1 A 0 A_StartSound("chargestyle/kick01", 6, CHAN_OVERLAP);
		TNT1 A 0 A_StartSound("*strongmelee", CHAN_VOICE);
		TNT1 A 0 A_Recoil(-5);		
		1126 E 1 ThrustThingZ(0,1,0,1);
		1126 E 1 ThrustThingZ(0,1,0,1);
		1126 E 1 ThrustThingZ(0,1,0,1);
		1126 E 1 ThrustThingZ(0,1,0,1);
		1126 E 1 ThrustThingZ(0,1,0,1);
		1126 E 1 ThrustThingZ(0,1,0,1);
		TNT1 A 0 A_Recoil(-5);		
		1126 F 1 A_TSP_Melee(200,90,"MagDivePuff",0,0, -300);
		Goto DeadlyAxeCont;
	DeadlyAxeGrounded:
		1126 F 1 A_TSP_Melee(200,90,"MagDivePuff",0,0, -300);
	DeadlyAxeCont:
		TNT1 A 0 A_SetBlend("39 99 0", 0.4, 20);
		TNT1 A 0 A_SetPitch(pitch + 10);
		1126 G 1;
		TNT1 A 0 Thrustthingz(0,-120,0,0);
	DeadlyAxeLoop: // I'm falling!
		TNT1 A 1 A_CheckFloor("DeadlyAxeFinish");
		Loop;
	DeadlyAxeFinish:
		TNT1 A 0 A_StartSound("magpunch/wall", 5, CHAN_OVERLAP);
	    TNT1 A 10 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_Jump(256, "AfterMelee");
		Goto AfterMelee;
	AfterMelee:
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99);
		Goto Select; // If I change it to "Goto Ready" it disappears, so...
	ModOff:
		TNT1 A 0 A_WeaponOffset(0,32);
		"----" A 0 A_Jump(256,"WaitForModSwitch"); //has 2 be a jump :pensive:
		Stop;
	ModOn:
		TNT1 A 0 A_WeaponOffset(0,32);
		TNT1 A 0 A_Raise;
		Goto Ready;
	}
}

/////////////////////////////////////////////////////
// Like the above, but for pew-pew weapons
/////////////////////////////////////////////////////

class TSP_MelRetro_LightWeapon : TSP_MelRetro_BaseWeapon
{
	States
	{
	SPTrait:
		TNT1 A 2 A_WeaponReady; //TNT1 A 7 A_WeaponReady
		Loop;
	}
}

/////////////////////////////////////////////////////
// For weapons that are unable to melee after doing a trait move
/////////////////////////////////////////////////////

class TSP_MelRetro_HeavyWeapon : TSP_MelRetro_BaseWeapon
{
	States
	{
	SPTrait:
		TNT1 A 7 A_WeaponReady;
		Loop;
	MagPullFinish:
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitCombo");
		TNT1 AAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOFIRE);
		Goto AfterMelee;
	MagPushFinish:
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitCombo");
		TNT1 AAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOFIRE);
		Goto AfterMelee;
	DeadlyAxeFinish:
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitCombo");
		Goto AfterMelee;
	}
}