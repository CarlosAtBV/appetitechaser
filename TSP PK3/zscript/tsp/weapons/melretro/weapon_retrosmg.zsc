//===========================================================================
//
// Kurdd & Schaumers AB-50
//
// by Cardboard Marty
// Sprites by Mike12, zrrion the insect, and Cardboard Marty
//
//===========================================================================

class MelRetroSMG : TSP_MelRetro_BaseWeapon
{
	DEFAULT
	{
	Tag "$MELRETROSMGTAG";
	Inventory.PickupMessage "$MELRETROSMGPICKUP";
	Obituary "%o learned why %k is the king of the iron fist.";
	Weapon.Kickback 100;
	Weapon.BobStyle "Alpha";
	Weapon.BobStyle "InverseSmooth";
	Weapon.BobSpeed 2.0;
	Weapon.BobRangeY 0.6;
	Weapon.BobRangeX 0.4;
	Weapon.AmmoType "TSPSMGAmmo";
	Weapon.AmmoGive 0;
	Weapon.AmmoUse 1;
	Inventory.Icon "ABXPA0";

		//TSP_Weapon_Core.ModTag "Duel-Wield"; //Used for "---- On/Off" message when switching mod fire on or off
		TSP_Weapon_Core.AmmoMult 1.2, 1.5, 2.0;
		TSP_Weapon_Core.ClipSize 50; //Initial starting size of clip
		TSP_Weapon_Core.FirstPickupLine "*ab50pickup"; //AAAAGH
		/*+TSP_Weapon_Core.MODFIREMODE //Activates modfire stuff
		-TSP_Weapon_Core.MODFIREZOOMS
		-TSP_Weapon_Core.HOLDDOWNFORMOD*/
		+WEAPON.NOAUTOFIRE
		+WEAPON.AMMO_OPTIONAL
		+NOEXTREMEDEATH
		+WEAPON.NOALERT
		+WEAPON.CHEATNOTWEAPON
	}
	States
	{
	Spawn:
		ABXP A -1;
		Stop;
	Select:
		TNT1 A 0
		{
			A_PlaySound("ab50/draw", 5);
			A_TakeInventory("MeleeSelected", 1);
			A_TakeInventory("WeaponSelectMemory", 999);
			A_GiveInventory("WeaponSelectMemory", 041);
		}
		TNT1 A 0 A_WeaponOffset(0,32);
		TNT1 A 4;
		ABXU ABCDE 1;
		ABXG A 1 A_Raise;
		Loop;
	Deselect:
		TNT1 A 0 A_PlaySound("ab50/holster", "5");
		TNT1 A 0 A_JumpIfInventory("ComboCooldown", 1, "ComboDeselect"); // Defined in weapon_base_melretro.zsc
		TNT1 A 0 A_TakeInventory("ComboCooldown", 99);
		ABXD ABCDE 1;
		TNT1 A 6;
	DeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower;
		TNT1 A 1 A_Lower;
		Loop;
	Ready:
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee");
		ABXG A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD);
		"####" A 0 A_TSP_GotoReadyState;
		Loop;
	Fire:
		TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty");
		TNT1 A 0 BRIGHT
		{
				A_GunFlash();
				A_PlaySound("ab50/fire", 6);
				A_TSP_FireBullets(5,1,-1,15,"TSP_BulletPuff",1);
				A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_SetPitch(pitch - .3);
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.2,0.2)));
		TNT1 A 0 A_TSP_WeaponRecoilDir(random(10,10),frandom(3,3));
		ASTF A 0 A_Jump(64,"AFire4");
		ASTF A 0 A_Jump(64,"AFire3");
		ASTF A 0 A_Jump(88,"Afire2");
	AFire1:
		TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-4,0,0,0);
		TNT1 A 0 A_Light2;
		ABXF A 1 BRIGHT A_Light1;
		TNT1 A 0 A_Light0;
		Goto FireEnd;
	AFire2:
		TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-3,0,0,0);
		TNT1 A 0 A_Light2;
		ABXF B 1 BRIGHT A_Light1;
		TNT1 A 0 A_Light0;
		Goto FireEnd;
	AFire3:
		TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-5,0,0,0);
		TNT1 A 0 A_Light2;
		ABXF C 1 BRIGHT A_Light1;
		TNT1 A 0 A_Light0;
		Goto FireEnd;
	AFire4:
		TNT1 A 0 A_FireProjectile("9mmCasingSpawner",-5,0,0,0);
		TNT1 A 0 A_Light2;
		ABXF D 1 BRIGHT A_Light1;
		TNT1 A 0 A_Light0;
		Goto FireEnd;
	FireEnd:
		ABXF E 1;
		ABXF F 1;
		ABXF G 2 A_Refire;
		ABXF FE 1;
		ABXG A 1;
		Goto Ready;
	Empty:
		TNT1 A 0 A_GiveInventory("Rechamber", 1);
		ABXG A 5 A_PlaySound("919m/dry", 5);
		"----" A 0 A_TSP_CheckReload();
		Goto Ready;
	Reload:
		ABXR AUBC 1;
		TNT1 A 0 A_FireProjectile("SpentClip1",3,0,5,0);
		ABXR D 1 A_PlaySound("ab50/reloadout", 5);
		ABXR EFGH 1;
		ABXR I 24 A_TSP_ReloadSpeed(24);
		TNT1 A 0 A_PlaySound("ab50/reloadin", 5);
		ABXR J 1 A_TSP_ReloadAmmo;
		ABXR KLMNOP 1;
		ABXR PSTCBUA 1;
		ABXG A 2;
		TNT1 A 0 A_JumpIfInventory("Rechamber", 1, "Rechambering");
		Goto Ready;
	Rechambering:
		TNT1 A 0 A_PlaySound("ab50/cock", 6);
		ABXC ABCCDEFG 1;
		ABXG A 3;
		TNT1 A 0 A_TakeInventory("Rechamber", 99);
		Goto Ready;

//Melee Start
	SPMelee:
		ABXG A 1;
		ABXD ABCDE 1;
	SPMeleeStart:
		TNT1 A 5;
		TNT1 A 0 A_PlaySound("strongmelee/miss", 5);
		TNT1 A 0 A_PlaySound("*melee", 6);
		ABXM ABC 1;
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk");
		ABXM D 1 A_TSP_Melee(24,90,"StrongMeleePuff");
		Goto SPMeleeEnd;
	SPMeleeBerserk:
		ABXM D 1 A_TSP_Melee(48,90,"StrongMeleePuff");
		Goto SPMeleeEnd;
	SPMeleeEnd: 
		ABXM EFGI 1;
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1);
		TNT1 A 8
		{
			A_WeaponReady(WRF_NOFIRE);
			if(CountInv("MeleePressed")==1)
			{
				return resolveState("SPMeleeStart");
			}
			return resolveState(null);
		}
	AfterMelee:
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99);
		ABXU ABCDE 1;
		Goto Ready;
	}
}