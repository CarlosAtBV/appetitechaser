Class TSP_GlobalDollarPickup_Base : CustomInventory
{
	int amountGiven;
	
	Property AmountGiven : amountGiven;
	
	Default
	{
		Inventory.PickupMessage "\cc[+\c[TSPLimeGreen]%i GBD\cc]";
		Inventory.PickupSound "money/pickup";
		+BRIGHT
		Scale 0.75;
	}
	
	int grabRadius;
	int mineSpeeden;
	bool movingToMeet;
	
	override void Tick()
	{
		super.Tick();
		if(Distance3D(players[0].mo)<64)
		{
			Touch(Actor(players[0].mo));
			Destroy();
			return;
		}
		if(movingToMeet)
		{
			if(mineSpeeden<32)
			{
				mineSpeeden++;
			}
			Vel3DFromAngle(mineSpeeden,AngleTo(players[0].mo),-atan2(players[0].mo.pos.z+24 - pos.z,Distance2D(players[0].mo)));
		}
		if(players[0].mo!=null&&players[0].mo.health>0&&Distance3D(players[0].mo)<grabradius)
		{
			movingToMeet = true;
			bNoClip = true;
		}
		if(grabradius<1024)
		{
			grabradius+=2;
		}
	}
	
	States
	{
		Spawn:
			GBL1 ABCDEFGH 2;
			Loop;
	}
	
	override bool TryPickup (in out Actor other)
	{
		if(super.TryPickup(other))
		{
			TSP_PlayerPawn_ZSCBase(other).AddLogMessage(PickupMsg,amountGiven);
			return true;
		}
		return false;
	}
	override String PickupMessage ()
	{
		return "";
	}
}

Class TSP_GlobalDollarPickup_1 : TSP_GlobalDollarPickup_Base
{
	Default
	{
		//Inventory.PickupMessage "\ccPicked up a Global Dollar [\cq$1\cc]";
		TSP_GlobalDollarPickup_Base.AmountGiven 1;
	}
	States
	{
		Pickup:
			TNT1 A 0 A_GiveInventory("TSP_GlobalDollars",1);
			Stop;
	}
}
Class TSP_GlobalDollarPickup_5 : TSP_GlobalDollarPickup_Base
{
	Default
	{
		//Inventory.PickupMessage "\ccPicked up a Global Dollar [\ck$5\cc]";
		Translation "0:255=%[0.72,0.28,0.16]:[1.90,1.25,0.54]";
		TSP_GlobalDollarPickup_Base.AmountGiven 5;
	}
	States
	{
		Pickup:
			TNT1 A 0 A_GiveInventory("TSP_GlobalDollars",5);
			Stop;
	}
}