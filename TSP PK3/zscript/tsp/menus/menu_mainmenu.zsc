Class TSP_MenuBase_ListButton
{
	string text;
	string desc;
	ListMenuItemSelectable item;
	Font fnt;
	Vector2 pos;
	Vector2 size;
	bool isSelected;
	double flash;
	Color color;
	
	TSP_MenuBase_ListButton buttonUp;
	TSP_MenuBase_ListButton buttonDown;
	
	Vector2 offset;
	
	void Tick()
	{
		offset.x = offset.x + ((isSelected ? 35 : 0) - offset.x) / 4;
		if ( flash > 0.0 )
		{
			flash -= 0.2;
		}
	}
}

Class TSP_ListMenuHiRes : ListMenu
{
	mixin TSP_HiResBase;
	
	Array<TSP_MenuBase_ListButton> tsp_buttons;
	
	TSP_MenuBase_ListButton mb_curSelection;
	double buttonFlash;
	
	double tsp_lerpX;
	double tsp_bgDim;
	
	ListMenuItemSelectable tsp_storedMenu;
	bool isQuitting;
	
	const MENUBUTX = 73;
	const MENUBUTY = 228;
	const MENUBUTH = 57;
	
	double easeOutBack (double x)
	{
		double c1 = 1.70158;
		double c3 = c1 + 1;

		return 1 + c3 * ((x - 1) ** 3) + c1 * ((x - 1) ** 2);
	}
	
	double easeOutExpo (double x)
	{
		return (x == 1) ? 1 : 1 - ((2) ** (-10 * x));
	}
	
	void AddButton(string name, string desc = "Add description pls", ListMenuItemSelectable item = null, Color color = Font.CR_UNTRANSLATED)
	{
		TSP_MenuBase_ListButton newButton;
		newButton = new("TSP_MenuBase_ListButton");
		newButton.text = StringTable.Localize(name).MakeUpper();
		newButton.desc = desc;
		newButton.item = item;
		newButton.fnt = "tsp_bold";
		newButton.color = color;
		newButton.size = (newButton.fnt.StringWidth(name), 35);
		newButton.pos = (MENUBUTX, MENUBUTY + MENUBUTH * tsp_buttons.Size());
		
		if ( tsp_buttons.Size() > 0 )
		{
			newButton.buttonUp = tsp_buttons[tsp_buttons.Size()-1];
			tsp_buttons[tsp_buttons.Size()-1].buttonDown = newButton;
		}
		
		tsp_buttons.Push(newButton);
	}
	
	override void Init(Menu parent, ListMenuDescriptor desc)
	{
		Super.Init(parent, desc);
		
		DontDim = true;
		
		tsp_buttons.Clear();
		
		Vector2 basePos = (MENUBUTX, MENUBUTY);
		
		for( int i = 0; i < mDesc.mItems.Size(); i++ )
		{
			let menuitem = ListMenuItemTextItem(mDesc.mItems[i]);
			if ( menuitem )
			{
				AddButton(menuitem.mText, menuitem.mText.."_SUB", ListMenuItemSelectable(mDesc.mItems[i]), Font.FindFontColor("TSPMenuGreen"));
			}
		}
		/*AddButton("Options", "Adjust game settings.", "TSP_OptionsMenu", Font.FindFontColor("TSP_Yellow"));
		AddButton("Codex", "Explore Mel's information database.", "TSP_OptionsMenu", Font.FindFontColor("TSPBlue"));
		AddButton("Load Game", "Load a saved game.", "LoadGameMenu", Font.FindFontColor("TSPMenuGreen"));
		AddButton("Save Game", "Save your current progress.", "SaveGameMenu", Font.FindFontColor("TSPMenuGreen"));
		AddButton("Shut Down", "Exit The Space Pirate.", "QuitMenu", Font.FindFontColor("TSPRed"));*/
		
		if ( tsp_buttons.Size() > 0 )
		{
			mb_curSelection = tsp_buttons[0];
		}
		
		tsp_bgDim = (mParentMenu == null) ? 0.0 : 1.0;
		tsp_storedMenu = null;
	}
	
	override void OnReturn()
	{
		tsp_storedMenu = null;
	}
	
	override void Ticker()
	{
		super.Ticker();
		
		if ( tsp_storedMenu == null && isQuitting == false )
		{
			if ( tsp_lerpX < 1.0 )
			{
				tsp_lerpX += 0.05;
			}
			
			if ( mParentMenu == null && tsp_bgDim < 1.0 )
			{
				tsp_bgDim += 0.1;
				if ( tsp_bgDim > 1.0 )
				{
					tsp_bgDim = 1.0;
				}
			}
		}
		/*else if ( tsp_lerpX > 0.0 )
		{
			
			if ( tsp_lerpX > 0.0 )
			{
				tsp_lerpX -= 0.1;
			}
			
			if ( (mParentMenu == null && isQuitting) && tsp_bgDim > 0.0 )
			{
				tsp_bgDim -= 0.1;
			}
			
			if ( tsp_lerpX <= 0 )
			{
				if ( isQuitting )
				{
					Close();
					let m = GetCurrentMenu();
					MenuSound(m != null ? "menu/backup" : "menu/clear");
					if (!m) menuDelegate.MenuDismissed();
				}
				else
				{
					tsp_storedMenu.Activate();
				}
			}
		}*/
		
		for( int i = 0; i < tsp_buttons.Size(); i++ )
		{
			tsp_buttons[i].isSelected = ( tsp_buttons[i] == mb_curSelection );
			tsp_buttons[i].Tick();
		}
	}
	
	override bool MouseEvent(int type, int x, int y)
	{
		// convert x/y from screen to virtual coordinates, according to CleanX/Yfac use in DrawTexture
		Vector2 mousePos = ConvertMousePos((x, y));
		
		TSP_MenuBase_ListButton newSel = null;
		
		for( int i = 0; i < tsp_buttons.Size(); i++ )
		{
			if ( CheckMousePos(mousePos, tsp_buttons[i].pos, tsp_buttons[i].size) )
			{
				newSel = tsp_buttons[i];
			}
		}
		
		if ( newSel != mb_curSelection )
		{
			if ( newSel != null )
			{
				newSel.flash = 1.0;
				S_Sound("menu/cursor",CHAN_UI);
			}
		}
		mb_curSelection = newSel;
		
		if ( type == Menu.MOUSE_Release )
		{
			if ( mb_curSelection != null )
			{
				MenuEvent(MKEY_Enter,false);
			}
			return true;
		}
		
		return Super.MouseEvent(type, x, y);
	}
	
	override bool MenuEvent (int mkey, bool fromcontroller)
	{
		if ( mb_curSelection == null )
		{
			mb_curSelection = tsp_buttons[0];
		}
		
		switch (mkey)
		{
			case MKEY_Up:
				if ( mb_curSelection.buttonUp != null )
				{
					mb_curSelection = mb_curSelection.buttonUp;
				}
				mb_curSelection.flash = 1.0;
				S_Sound("menu/cursor",CHAN_UI);
				return true;
				
			case MKEY_Down:
				if ( mb_curSelection.buttonDown != null )
				{
					mb_curSelection = mb_curSelection.buttonDown;
				}
				mb_curSelection.flash = 1.0;
				S_Sound("menu/cursor",CHAN_UI);
				return true;
				
			case MKEY_Enter:
				S_Sound("menu/choose",CHAN_UI);
				if ( mb_curSelection != null )
				{
					mb_curSelection.item.Activate();
					//tsp_storedMenu = mb_curSelection.item;
					//SetMenu(mb_curSelection.menu, 0);
					return true;
				}
				
			case MKEY_Back:
				Close();
				let m = GetCurrentMenu();
				MenuSound(m != null ? "menu/backup" : "menu/clear");
				if ( !m )
				{
					menuDelegate.MenuDismissed();
				}
				else if ( TSP_ListMenuHiRes(m) )
				{
					TSP_ListMenuHiRes(m).tsp_lerpX = 0.0;
				}
				//isQuitting = true;
				//tsp_storedMenu = "shut the fuck up";
				return true;
		}
		return super.MenuEvent(mkey, fromcontroller);
	}
	
	override void Drawer()
	{
		UpdateViewport();
		
		Vector2 menuOffset = (-640 + 640 * easeOutExpo(tsp_lerpX), 0);
		
		screen.Dim("#000000", tsp_bgDim * 0.5, 0, 0, screen.GetWidth(), screen.GetHeight());
		
		//screen.Dim("#00FF00", 1.0, 0, 0, screen.GetWidth(), screen.GetHeight());
			
		/*screen.DrawText("tsp_bold", Font.CR_WHITE, 0, 0, ""..CVar.FindCVar("playerclass").GetString(),
			DTA_VirtualWidth, 1280, DTA_VirtualHeight, 720, DTA_KeepRatio, true,
			DTA_ViewportX, int(viewportPos.x), DTA_ViewportY, int(viewportPos.y),
			DTA_ViewportWidth, int(viewportSize.x), DTA_ViewportHeight, int(viewportSize.y));*/
		
		Font fnt = "tsp_bold";
		String testString = "FUCK";
		for ( int i = 0; i < tsp_buttons.Size(); i++ )
		{
			let button = tsp_buttons[i];
			
			screen.DrawText(fnt, button.color, menuOffset.x + button.pos.x + button.offset.x + 4, button.pos.y, button.text,
				DTA_VirtualWidth, 1280, DTA_VirtualHeight, 720, DTA_KeepRatio, true,
				DTA_ViewportX, int(viewportPos.x), DTA_ViewportY, int(viewportPos.y),
				DTA_ViewportWidth, int(viewportSize.x), DTA_ViewportHeight, int(viewportSize.y));
				
			if ( button.flash > 0.0 )
			{
				screen.DrawText(fnt, button.color, menuOffset.x + button.pos.x + button.offset.x + 4, button.pos.y, button.text,
					DTA_LegacyRenderStyle, STYLE_Add, DTA_Alpha, button.flash,
					DTA_VirtualWidth, 1280, DTA_VirtualHeight, 720, DTA_KeepRatio, true,
					DTA_ViewportX, int(viewportPos.x), DTA_ViewportY, int(viewportPos.y),
					DTA_ViewportWidth, int(viewportSize.x), DTA_ViewportHeight, int(viewportSize.y));
			}
		}
		
		if ( mb_curSelection )
		{
			screen.DrawText("tsp_small", Font.CR_UNTRANSLATED, menuOffset.x + 87, 579, mb_curSelection.desc,
				DTA_VirtualWidth, 1280, DTA_VirtualHeight, 720, DTA_KeepRatio, true,
				DTA_ViewportX, int(viewportPos.x), DTA_ViewportY, int(viewportPos.y),
				DTA_ViewportWidth, int(viewportSize.x), DTA_ViewportHeight, int(viewportSize.y));
		}
	}
}