const TSP_MENUHEADERY = 74;

Class TSP_UIButton
{
	Vector2 pos;
	Vector2 size;
	string text;
	
	Vector2 vis_offset;
	Vector2 vis_offset_targ;
	Vector2 vis_offset_lerp;
	
	virtual void Tick(float delta, int state)
	{
		vis_offset = vis_offset + ( vis_offset_targ - vis_offset ) * (delta * 12);
		
		if ( state == 1 )
		{
			vis_offset_targ = (35,0);
		}
		else
		{
			vis_offset_targ = (0,0);
		}
	}
	
	Vector2 GetPos()
	{
		return pos + vis_offset;
	}
}

Mixin Class TSP_UIDrawMixin
{
	enum TSPDrawTextureFlags
	{
		TDF_ANCHOR_LEFT = 0,
		TDF_ANCHOR_RIGHT = 0x1,
	}
	
	enum TSPDrawTextFlags
	{
		TDF_ALIGN_LEFT = 0,
		TDF_ALIGN_RIGHT = 0x10,
		TDF_ALIGN_CENTER = 0x20,
	}
	
	TSP_TextColorBrain ui_textcolors;
	double ui_scale;
	Vector2 ui_offset;
	float ui_delta;
	float ui_prevMS;
	
	void UpdateMenuStuff()
	{
		if ( ui_textcolors == null )
		{
			ui_textcolors = ui_textcolors.Get();
		}
		ui_scale = screen.GetHeight() / 720.0;
		ui_delta = (Menu.GetCurrentMenu().MSTime() - ui_prevMS) / 1000.;
		ui_prevMS = Menu.MSTime();
		//Console.Printf(""..ui_delta);
	}
	
	Vector2 ConvertMousePos(Vector2 mouse)
	{
		Vector2 getPos = mouse / ui_scale;
		return getPos;
	}
	
	bool CheckMousePos(Vector2 mousePos, Vector2 start, Vector2 size)
	{
		return (mousePos.x >= start.x && mousePos.y >= start.y) && (mousePos.x < start.x+size.x && mousePos.y < start.y+size.y);
	}

	void UI_DrawImage (String image, Vector2 pos, int flags = 0, float alpha = 1.0)
	{
		let tex = TexMan.CheckForTexture(image);
		UI_DrawTexture(tex, pos, flags, alpha);
	}
	
	void UI_DrawTexture (TextureID tex, Vector2 pos, int flags = 0, float alpha = 1.0)
	{
		pos *= ui_scale;
		pos += ui_offset;
		if ( flags & TDF_ANCHOR_RIGHT )
		{
			pos.x += screen.GetWidth();
		}
		screen.DrawTexture(tex, true, pos.x, pos.y,
			DTA_ScaleX, ui_scale, DTA_ScaleY, ui_scale,
			DTA_Alpha, alpha);
	}
	
	void UI_DrawString (String getFont, String string, Vector2 pos, int flags = 0, Color color = Color(255,255,255,255), float alpha = 1.0, int wrapwidth = -1, int linespacing = 4, Vector2 scale = (1,1), int pt = 0, ERenderStyle style = STYLE_Translucent)
	{
		string = StringTable.Localize(string);
		
		Font fnt = Font.GetFont(getFont);
		if ( fnt == null ) ThrowAbortException("Unknown font: '"..getFont.."'");
		int kerning = fnt.GetDefaultKerning();
		
		if ( flags & TDF_ALIGN_RIGHT )
		{
			pos.x -= fnt.StringWidth(string);
			//pos.x -= fnt.GetCharWidth(string.ByteAt(0));
		}
		else if ( flags & TDF_ALIGN_CENTER )
		{
			pos.x -= (fnt.StringWidth(string))/2;
			//pos.x -= fnt.GetCharWidth(string.ByteAt(0)) / 2;
		}
		
		pos *= ui_scale;
		pos += ui_offset;
		
		if ( flags & TDF_ANCHOR_RIGHT )
		{
			pos.x += screen.GetWidth();
		}
		
		Color newColor = color;
		
		for ( int i = 0; i < string.Length(); i++ )
		{
			if ( string.ByteAt(i) == 28 )
			{
				if ( string.ByteAt(i+1) == 91 )
				{
					//Console.Printf("~ [] Color Type");
					int startPos = i+2;
					int bt = startPos;
					while( bt < string.Length() && string.ByteAt(bt) != 93 )
					{
						bt++;
					}
					bt -= startPos;
					newColor = Color(ui_textcolors.colors.At(string.Mid(startPos, bt)));
					Console.Printf(string.Mid(startPos, bt).." "..newColor);
					newColor = Color(255, newColor.r, newColor.g, newColor.b);
					//newColor = Color(255, int(127+127*sin(level.maptime*12)), 0, 0);
					i = startPos+bt;
					continue;
				}
				else
				{
					//Console.Printf("~ c Color Type");
					newColor = Color(255, 255, 255, 255);
					i += 1;
					continue;
				}
			}
			string charTex = "fonts/"..getFont.."/"..(String.Format("%04x", string.ByteAt(i)))..".png";
			TextureID tex = TexMan.CheckForTexture(charTex);
			/*screen.DrawTexture(tex, true, pos.x, pos.y, DTA_ScaleX, ui_scale, DTA_ScaleY, ui_scale,
				DTA_Alpha, alpha, DTA_Color, Color(255, newColor.r / 2, newColor.g / 4, newColor.b / 6));
			screen.DrawTexture(tex, true, pos.x, pos.y, DTA_ScaleX, ui_scale, DTA_ScaleY, ui_scale,
				DTA_Alpha, alpha, DTA_FillColor, newColor, DTA_AlphaChannel, true);*/
			screen.DrawTexture(tex, true, pos.x, pos.y,
				DTA_ScaleX, ui_scale, DTA_ScaleY, ui_scale,
				DTA_Alpha, alpha, DTA_Color, newColor);
			pos.x += ( fnt.GetCharWidth(string.ByteAt(i)) + kerning ) * ui_scale;
		}
		
		/*TextureID tex = TexMan.CheckForTexture("fonts/tsp_bold/003F.png");
		screen.DrawTexture(tex, true, pos.x, pos.y-32,
			DTA_ScaleX, 4, DTA_ScaleY, 4);*/
		//screen.DrawText(fnt, Font.CR_UNTRANSLATED, pos.x, pos.y+32, string);
	}
}