//===========================================================================
//
// Makoto Arms Type 917
//
// by Cardboard Marty
// Sprites by Sgt. Shivers and Cardboard Marty
//
//===========================================================================

ACTOR RoboMel917Pistol : TSPMelLightWeapon
{
  Weapon.Kickback 110
  Weapon.AmmoType "AutoPistol917Mag"
  Weapon.AmmoGive 0
  Weapon.AmmoUse 1
  Weapon.AmmoType2 "TSPPistolAmmo"
  Weapon.AmmoGive2 0
  Weapon.AmmoUse2 0
  Weapon.BobStyle InverseSmooth
  Weapon.BobSpeed 2.4
  Weapon.BobRangeY 0.5
  Weapon.BobRangeX 0.7
  Inventory.PickupMessage "[Makoto Type 917] [Class: Pistol, Slot 2]"
  Obituary "Target %o has been taken out by %k. Assignment Complete."
  +NOAUTOFIRE
  +AMMO_OPTIONAL
  +NOEXTREMEDEATH
  +WEAPON.NoAlert
  +WEAPON.CHEATNOTWEAPON
  Tag "Makoto Arms 917"
  States
  {
  Spawn:
    221P A -1
    Stop
  Select:
    TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
    TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
    TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 221)
    TNT1 A 0 A_PlaySound("917/select", 5)
    TNT1 A 3 SetPlayerProperty(0,1,2)
	TNT1 A 0 A_JumpIfInventory("917Jam", 1,"JamSelect")
    221D EDCBA 1
  SelectLoop:
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
    221G A 1 A_Raise
    Loop
  JamSelect:
    221D JIHGF 1
  JamSelectLoop:
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
    221J C 1 A_Raise
    Loop
  Deselect:
    TNT1 A 0 A_JumpIfInventory("917Jam", 1,"JamDeselect")
    TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
    TNT1 A 0 A_PlaySound("917/deselect", 5)
    221D ABCDE 1
  DeselectLoop:
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
    TNT1 A 1 A_Lower
    Loop
  JamDeselect:
    221D FGHIJ 1
	Goto DeselectLoop
  Ready:
    TNT1 A 0 A_JumpIfInventory("917Jam", 1,"JamReady")
    TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMelee")
    TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitStart")
    221G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    LOOP
  JamReady:
    TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMelee")
    TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitStart")
    221J C 1 A_WeaponReady
	Loop
  Fire:
    TNT1 A 0 A_JumpIfInventory("917Jam", 1,"Unjam")
    TNT1 A 0 A_JumpIfNoAmmo("Empty")
    221F A 1 BRIGHT
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SetPitch(pitch - 0.7)
    TNT1 A 0 A_SetAngle(angle + (frandom(-0.5,0.5)))
    TNT1 A 0 A_PlaySound("917/fire", 5)
    TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
	TNT1 A 0 A_Jump(3,"JammedFire")
  AFire0:
    TNT1 A 0 BRIGHT A_FireBullets(4,4,-1,7,"919mBulletPuff",1)
    TNT1 A 0 A_Jump(64,"AFire3")
    TNT1 A 0 A_Jump(88,"Afire2")
  AFire1:
    TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
    TNT1 A 0 A_Light2
    221F B 1 BRIGHT A_Light1
    TNT1 A 0 A_Light0
    TNT1 A 0 A_Refire
    Goto FireEnd
  AFire2:
    TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
    TNT1 A 0 A_Light2
    221F C 1 BRIGHT A_Light1
    TNT1 A 0 A_Light0
    TNT1 A 0 A_Refire
    Goto FireEnd
  AFire3:
    TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
    TNT1 A 0 A_Light2
    221F D 1 BRIGHT A_Light1
    TNT1 A 0 A_Light0
    TNT1 A 0 A_Refire
  FireEnd:
    TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
    TNT1 A 0
    221G DCB 1
    Goto Ready
  JammedFire:
	TNT1 A 0 A_GiveInventory("917Jam", 1)
    TNT1 A 0 BRIGHT A_FireBullets(4,4,-1,7,"919mBulletPuff",1)
    TNT1 A 0 A_Light2
    221F B 1 BRIGHT A_Light1
    TNT1 A 0 A_Light0
	TNT1 A 0 A_PlaySound("917/jam", 6)
	221J ABCCCC 1
	Goto JamReady
  Unjam:
	221J CDEFFFFFFFF 1
	TNT1 A 0 A_PlaySound("917/clear", 6)
	TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
	221J GHIJKKKLLMNO 1
	221G A 3
	TNT1 A 0 A_TakeInventory("917Jam", 99)
	Goto Ready
  Empty:
    221F A 5 A_PlaySound("919m/dry", 6)
	Goto Reload
  Reload:
	TNT1 A 0 A_JumpIfInventory("AutoPistol917Mag", 30, "2")
	TNT1 A 0 A_JumpIfInventory("TSPPistolAmmo", 1, "2")
	221G A 1 A_WeaponReady(WRF_NOFIRE)
	Goto Ready
  ReloadStart:
	  TNT1 A 0 A_TakeInventory("ShotsFiredPewPew", 99)
      221G BCD 1
	  221R ABCD 1
	  TNT1 A 0 A_FireCustomMissile("SpentClip1",3,0,5,0)
	  221R E 1 A_PlaySound("917/magout", 6)
	  221R FG 1
	  221R HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 1
	  TNT1 A 0 A_PlaySound("917/magin1", 6)
	  221R IJKL 1
    ReloadWork:
	  TNT1 A 0 A_TakeInventory("TSPPistolAmmo",1)
	  TNT1 A 0 A_GiveInventory("AutoPistol917Mag",1)
	  TNT1 A 0 A_JumpIfInventory("AutoPistol917Mag",30,"ReloadFinish")
	  TNT1 A 0 A_JumpifInventory("TSPPistolAmmo",1,"ReloadWork")
	ReloadFinish:
	  221R MNNOP 1
	  221R Q 1 A_PlaySound("917/magin2", 5)
	  221R RRRRRRSTUVWX 1
	  221G A 3
	  Goto Ready
	AfterCombo:
	  TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
	  TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
	  TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
	  TNT1 A 0 A_TakeInventory("MelMachCombo", 99)
	  ASTU ABCD 1
	  Goto Ready
//Melee Start
  SPMelee:
    TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoModeMelee")
    MPTD ABCD 1
  SPMeleeStart:
    TNT1 A 0 A_Jump(128,"Melee2")
    TNT1 A 7
    TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
    MPTM ABCDE 1
    ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
    MPTM F 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
    Goto SPMeleeA
  SPMeleeBerserk:
    MPTM F 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
    Goto SPMeleeA
  SPMeleeA:    
    MPTM GHI 1
    TNT1 A 6
    TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
    TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoMeleeCooldown")
    MPTS ABCD 1
    Goto Ready
  Melee2:
    TNT1 A 7
    TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
    MPTM JKLM 1
    ML1F D 0 A_JumpIfInventory("PowerStrength",1,"Melee2Berserk")
    MPTM N 1 A_CustomPunch(8,0,0,"StrongMeleePuff",90)
    Goto Melee2A
  Melee2Berserk:
    MPTM N 1 A_CustomPunch(16,0,0,"StrongMeleePuff",90)
    Goto Melee2A
  Melee2A: 
    MPTM OPQR 1
    TNT1 A 6
    TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
    TNT1 A 0 A_JumpIfInventory("AutoPistolFullAuto",1,"AutoMeleeCooldown")
    MPTS ABCD 1
    Goto Ready
//Auto Mode Melee
  AutoModeMelee:
    ASTD ABCD 1
  AutoModeMeleeStart:
    TNT1 A 0 A_Jump(256,"SPMeleeStart")
  AutoMeleeCooldown:
    TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMeleeStart")
    ASTU EFGH 1
    Goto Ready
  }
}

ACTOR AutoPistol917Mag : Ammo
{
  +IGNORESKILL
  Inventory.MaxAmount 30
}

ACTOR ShotsFiredPewPew917 : Inventory
{
  +IGNORESKILL
  Inventory.MaxAmount 30
}

ACTOR 917Jam : Inventory
{
  +IGNORESKILL
  Inventory.MaxAmount 1
}