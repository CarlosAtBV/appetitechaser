//===========================================================================
//
// OS06 Multi-Purpose SMG "Suzuka"
//
// by Cardboard Marty
// Sprites by Cardboard Marty
// Model Bases:
// - AE4 HMG Short: ported by Kalash
//   * From "Call of Duty: Advanced Warfare"
// - D13 Sector: ported by saltpowered
//   * From "Call of Duty: Black Ops 3"
// - CPS Sight by ProgrammerNetwork
// - RIS Grip by ProgrammerNetwork
// - Cobra Aimpoint by ProgrammerNetwork
// - NcStar Sight by ProgrammerNetwork
// - FNC Stock by ProgrammerNetwork
//
//===========================================================================

ACTOR MelSMG : TSPMelLightWeapon
{
	Inventory.PickupMessage "$MELSMGPICKUP"
	Obituary "%o learned why %k is the king of the iron fist."
	Weapon.Kickback 80
	Weapon.BobStyle Alpha
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.8
	Weapon.BobRangeY 0.3
	Weapon.BobRangeX 0.5
	Weapon.AmmoType "TSPSMGAmmo"
	Weapon.AmmoGive 0//0
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 8
	Inventory.Icon "131PA0"

		TSP_Weapon_Core.AmmoMult 1.35, 1.7, 2.0
		TSP_Weapon_Core.ClipSize 40 //Initial starting size of clip
		+TSP_Weapon_Core.MODFIREMODE //Activates modfire stuff
		TSP_Weapon_Core.ModTag "Shotgun" 
		TSP_Weapon_Core.FirstPickupLine "mel/suzukapickup"
		
	Tag "$MELSMGTAG"
	+NOAUTOFIRE
	+AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.NoAlert
	States
	{
	Spawn:
		131P A -1
		Stop
	Select:
		TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
		TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
		TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 131)
		TNT1 A 0 A_PlaySound("919m/select", 5)
		TNT1 A 0 A_WeaponOffset(0,32)
		131G A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("MagPushToken", 99)
		TNT1 A 0 A_TakeInventory("MagSlingToken", 99)
		TNT1 A 0 A_PlaySound("919m/deselect", 5)
		131G A 1
	DeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
		TNT1 A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait")
		131G A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD)
		"----" A 0 A_TSP_GotoReadyState
		Loop
	ModReady:
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait")
		131G C 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD)
		"----" A 0 A_TSP_GotoReadyState
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("TSPTopFired", 1, "BottomFire")
		TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty")
		TNT1 A 0 A_PlaySound("suzuka/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0
		{
			A_TSP_FireBullets(2,4,-1,7,"BulletPuff",1);
			A_TSP_TakeClip(1);
			A_TSP_WeaponRecoilDir(random(35,55),frandom(4,9));
		}
		TNT1 A 0 A_SetPitch(pitch - 0.4)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.1,0.3)))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
		131G A 1 BRIGHT A_Light2
		131G B 1 A_GiveInventory("TSPTopFired", 1)
		131G A 1
		TNT1 A 0 A_Refire("Fire")
		"----" A 0 A_TSP_GotoReadyState
		Goto Ready
	BottomFire:
		TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty")
		TNT1 A 0 A_PlaySound("suzuka/fire", 6)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0
		{
			A_TSP_FireBullets(2,4,-1,7,"BulletPuff",1,0,"TSP_HitscanTracer",-4);
			A_TSP_TakeClip(1);
			A_TSP_WeaponRecoilDir(random(35,55),frandom(4,9));
		}
		TNT1 A 0 A_SetPitch(pitch - 0.3)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.3,0.1)))
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
		131G B 1 BRIGHT A_Light2
		131G B 1 A_TakeInventory("TSPTopFired", 1)
		131G A 1
		TNT1 A 0 A_Refire("Fire")
		"----" A 0 A_TSP_GotoReadyState
		Goto Ready
	ModFire:
		TNT1 A 0 A_TSP_JumpIfClipLower(8,"Empty")
		TNT1 A 0
		{
			A_PlaySound("suzuka/firea", 6);
			A_AlertMonsters;
			A_Quake(2,3,0,5,"");
			A_TSP_FireBullets(6,0,8,7,"BulletPuff",1,0,"",-2);
			A_TSP_TakeClip(8);
			A_SetPitch(pitch - 2.5);
			A_SetAngle(angle + (frandom(-0.5,0.5)));
		}
		131G D 1 BRIGHT
		{
			A_Light2();
			A_TSP_WeaponRecoilDir(45,frandom(24,32));
		}
		131G D 1
		{
			A_TSP_WeaponRecoilDir(random(35,55),frandom(40,48),1);
		}
		131G D 4
		131G E 2
		131G C 3
		131G CFG 1 A_TSP_WeaponRecoilDir(45,-2,1)
		//cocking anim
		"----" A 0
		{
			A_PlaySound("suzuka/scock", 5);
			A_TSP_WeaponRecoilDir(random(35,55)-10,18,1);
		}
		131G GGGF 2 A_TSP_WeaponRecoilDir(35,4,1)
		//ok done
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-7,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-10,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-11,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-6,0,0,0)
		"----" A 0 A_TSP_GotoReadyState
		Goto Ready
	Empty:
		131G A 10 A_PlaySound("suzuka/empty", 5)
		"----" A 0 A_TSP_CheckReload()
		Goto Ready
	Reload:
		131G A 10
		TNT1 A 0 A_PlaySound("suzuka/eject", 6)
		131G A 25
		TNT1 A 0 A_TSP_ReloadAmmo
		TNT1 A 0 A_PlaySound("suzuka/load", 5)
		131G A 15
		TNT1 A 0 A_PlaySound("suzuka/rcock", 6)
		131G A 5
		Goto Ready
	}
}

///////////////////////
// SMG SHENANIGANS
///////////////////////
ACTOR TSPTopFired : Ammo
{
  +IGNORESKILL
  Inventory.MaxAmount 1
}