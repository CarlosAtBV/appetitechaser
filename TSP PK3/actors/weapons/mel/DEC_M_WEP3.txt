//===========================================================================
//
// OS06 Multi-Purpose SMG "Suzuka"
//
// by Cardboard Marty
// Sprites by Cardboard Marty
// Model Bases:
// - AE4 HMG Short: ported by Kalash
//   * From "Call of Duty: Advanced Warfare"
// - D13 Sector: ported by saltpowered
//   * From "Call of Duty: Black Ops 3"
// - CPS Sight by ProgrammerNetwork
// - RIS Grip by ProgrammerNetwork
// - Cobra Aimpoint by ProgrammerNetwork
// - NcStar Sight by ProgrammerNetwork
// - FNC Stock by ProgrammerNetwork
//
//===========================================================================

ACTOR MelSMG : TSPMelLightWeapon
{
  Inventory.PickupMessage "$MELSMGPICKUP"
  Obituary "%o learned why %k is the king of the iron fist."
  Weapon.Kickback 80
  Weapon.BobStyle Alpha
  Weapon.BobStyle InverseSmooth
  Weapon.BobSpeed 1.8
  Weapon.BobRangeY 0.3
  Weapon.BobRangeX 0.5
  Weapon.AmmoType "TSPSMGAmmo"
  Weapon.AmmoGive 0//0
  Weapon.AmmoUse 1
  Inventory.Icon "121PA0"
  
  TSP_Weapon_Core.AmmoMult 1.35, 1.7, 2.0
  TSP_Weapon_Core.ClipSize 40 //Initial starting size of clip
  +TSP_Weapon_Core.MODFIREMODE //Activates modfire stuff
  TSP_Weapon_Core.ModTag "Shotgun" 
  Tag "$MELSMGTAG"
  +NOAUTOFIRE
  +AMMO_OPTIONAL
  +NOEXTREMEDEATH
  +WEAPON.NoAlert
  States
  {
  Spawn:
    121P A -1
    Stop
  Select:
    TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
    TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
    TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 131)
    TNT1 A 0 A_PlaySound("919m/select", 5)
    TNT1 A 0 SetPlayerProperty(0,1,2)
  SelectLoop:
    131G A 0 A_Raise
    131G A 1 A_Raise
    Loop
  Deselect:
    TNT1 A 0 A_TakeInventory("MagPushToken", 99)
    TNT1 A 0 A_TakeInventory("MagSlingToken", 99)
    TNT1 A 0 A_PlaySound("919m/deselect", 5)
	131G A 1
  DeselectLoop:
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
    TNT1 A 1 A_Lower
    Loop
  Ready:
    TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
    TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTrait")
    131G A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD)
    LOOP
  Fire:
    TNT1 A 0 A_JumpIfInventory("TSPTopFired", 1, "BottomFire")
	TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty")
    TNT1 A 0 A_PlaySound("suzuka/fire", 5)
    TNT1 A 0 A_AlertMonsters
	TNT1 A 0
	{
		A_TSP_FireBullets(2,4,-1,7,"BulletPuff",1);
		A_TSP_TakeClip(1);
	}
	TNT1 A 0 A_SetPitch(pitch - 0.4)
	TNT1 A 0 A_SetAngle(angle + (frandom(-0.1,0.3)))
	131F A 1 BRIGHT A_Light2
	131F B 1 A_GiveInventory("TSPTopFired", 1)
	131G A 1
	TNT1 A 0 A_Refire("Fire")
	Goto Ready
  BottomFire:
	TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty")
    TNT1 A 0 A_PlaySound("suzuka/fire", 6)
    TNT1 A 0 A_AlertMonsters
	TNT1 A 0
	{
		A_TSP_FireBullets(2,4,-1,7,"BulletPuff",1);
		A_TSP_TakeClip(1);
	}
	TNT1 A 0 A_SetPitch(pitch - 0.3)
	TNT1 A 0 A_SetAngle(angle + (frandom(-0.3,0.1)))
	131F C 1 BRIGHT A_Light2
	131F D 1 A_TakeInventory("TSPTopFired", 1)
	131G A 1
	TNT1 A 0 A_Refire("Fire")
	Goto Ready
  ModFire:
  	TNT1 A 0 A_TSP_JumpIfClipLower(6,"Empty")
    TNT1 A 0 A_PlaySound("suzuka/firea", 6)
    TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_Quake(2,3,0,5,"")
	TNT1 A 0
	{
		A_TSP_FireBullets(6,0,8,7,"BulletPuff",1);
		A_TSP_TakeClip(8);
	}
	TNT1 A 0 A_SetPitch(pitch - 2.5)
	TNT1 A 0 A_SetAngle(angle + (frandom(-0.5,0.5)))
	131F C 1 BRIGHT A_Light2
	131F D 1
	131G A 12
	131G A 12 A_PlaySound("suzuka/scock", 5)
	Goto Ready
  Empty:
    131G A 10 A_PlaySound("suzuka/empty", 5)
	"----" A 0 A_TSP_CheckReload()
    Goto Ready
  Reload:
    131G A 10
	TNT1 A 0 A_PlaySound("suzuka/eject", 6)
	131G A 25
   	TNT1 A 0 A_TSP_ReloadAmmo
	TNT1 A 0 A_PlaySound("suzuka/load", 5)
	131G A 15
	TNT1 A 0 A_PlaySound("suzuka/rcock", 6)
	131G A 5
	Goto Ready
  }
}

///////////////////////
// SMG SHENANIGANS
///////////////////////
ACTOR TSPTopFired : Ammo
{
  +IGNORESKILL
  Inventory.MaxAmount 1
}