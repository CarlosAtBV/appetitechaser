//===========================================================================
//
// Darling Nikki (Spitfire)
//
// by Cardboard Marty
// Sprites by Cardboard Marty
// Additional sprites by Jetplane (Flashes)
// Model Bases:
// - 2019 Detective Special: ported by Programmer Network
//   * Model by Toasty Fresh 
// - Atlas 45 (Custom) and Atlas 45 (Default): ported by Kalash
//   * From "Call of Duty: Advanced Warfare"
// - AE4 HMG Short: ported by Kalash
//   * From "Call of Duty: Advanced Warfare"
//
//===========================================================================

ACTOR MelPistolA : MelPistol
{
	Tag "$MELPISTOLATAG"
	+TSP_Weapon_Core.MODFIREMODE //Activates modfire stuff
	TSP_Weapon_Core.ModTag "Spitfire" //Used for "---- On/Off" message when switching mod fire on or off
	States
	{
		ModFire:  
			TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty")
			121G A 1 BRIGHT
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_SetPitch(pitch - 0.8)
			TNT1 A 0 A_SetAngle(angle + (frandom(-0.3,0.3)))
			TNT1 A 0 A_PlaySound("nikki/firea", 5)
		AFire0:
			TNT1 A 0 BRIGHT A_TSP_FireBullets(6,6,-1,9,"BulletPuff",1)
			TNT1 A 0 A_Jump(64,"AFire3")
			TNT1 A 0 A_Jump(88,"Afire2")
		AFire1:
			TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
			TNT1 A 0 A_Light2
			121F A 1 BRIGHT A_Light1
			TNT1 A 0 A_Light0
			TNT1 A 0 A_Refire
			Goto FireEnd
		AFire2:
			TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
			TNT1 A 0 A_Light2
			121F B 1 BRIGHT A_Light1
			TNT1 A 0 A_Light0
			TNT1 A 0 A_Refire
			Goto FireEnd
		AFire3:
			TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
			TNT1 A 0 A_Light2
			121F A 1 BRIGHT A_Light1
			TNT1 A 0 A_Light0
			TNT1 A 0 A_Refire
		FireEnd:
			121F CDEEF 1
			Goto Ready
	}
}