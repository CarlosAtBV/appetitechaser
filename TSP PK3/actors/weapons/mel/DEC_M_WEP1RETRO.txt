//===========================================================================
//
// Mel's Melee Style
// (Adapted from TSP0.1.2e and the unreleased TSPX)
//
// by Cardboard Marty
// Sprites by Cardboard Marty, Blox, Scuba Steve, Banjo Entertainment
// 
//
//===========================================================================

ACTOR MelRetroMelee : TSP_MelRetro_BaseWeapon
{
	Inventory.PickupMessage "You relearned your fighting style you apparently dropped [Class: Melee, Slot 1]"
	Obituary "%o learned why %k is the king of the iron fist."
	Weapon.Kickback 100
	Weapon.BobStyle Alpha
	Weapon.BobSpeed 1.5
	Weapon.BobRangeY 1.0
	Weapon.BobRangeX 0.5
	Tag "$MelRetroMeleeTag"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+NOEXTREMEDEATH
	+INVENTORY.UNDROPPABLE
	+TSP_Weapon_Core.NoModSwitch
	States
	{
	Spawn:
		TNT1 A -1
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo2", 99)
		TNT1 A 0 A_TakeInventory("MelMachCombo", 99)
		TNT1 A 0 A_JumpIfInventory("ComboCoolDown", 1, "ComboDeselect")
		TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "ComboDeselect")
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
		TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
		011D ABCD 1
		TNT1 A 3
	DeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
		TNT1 A 1 A_Lower
		Loop
	ComboDeselect:
		TNT1 A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_GiveInventory("MeleeSelected", 1)
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 4
		011U ABCD 1
		011G A 1 A_Raise
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitStart")
		011G A 1 A_WeaponReady
		Loop

// Punch!
	Fire:
		TNT1 A 0 A_JumpIf(CountInv("TSP_Upgrade_KeepAwayCombo")<1,"FireCont") // Keep Away Combo not purchased
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo2",3,"KeepAway")
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo2",2,"CrossDuo")
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo2",1,"CrossPunch")
	FireCont:
		TNT1 A 0 A_JumpIfInventory("MelMachCombo",4,"AfterMelee") //made so you can't knifehand again after the Rapid Fire Finish
		TNT1 A 0 A_JumpIfInventory("MelMachCombo",3,"RapidFireFinish")			// K-K-P-P-P
		TNT1 A 0 A_JumpIfInventory("MelMachCombo",2,"CrossDuo")					// K-K-P-P-P
		TNT1 A 0 A_JumpIfInventory("MelMachCombo",1,"CrossPunch")				// K-K-P-P
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo",2,"KnifeHand")			// K-K-P
		TNT1 A 0 A_JumpIfInventory("MelPrimaryCombo",3,"ScorpionTailFinisher")	// P-P-P-P
		TNT1 A 0 A_JumpIfInventory("MelPrimaryCombo",2,"LeftOverhead")			// P-P-P
		TNT1 A 0 A_JumpIfInventory("MelPrimaryCombo",1,"RightHook")				// P-P

// P DEFAULT		
	Punch1:	
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		0111 ABCD 1
		0111 D 0 A_JumpIfInventory("PowerStrength",1,"PunchPowered")
		0111 E 1 A_TSP_Melee(20,70,"BasicMeleePuff")
		Goto PunchEnd
	PunchPowered:
		0111 E 1 A_TSP_Melee(40,70,"BasicMeleePuff")
		Goto PunchEnd
		
// P END
	PunchEnd:
		TNT1 A 0 A_GiveInventory("MelPrimaryCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		0111 FGJK 1 A_WeaponReady
		TNT1 A 12 A_WeaponReady
		Goto AfterMelee
		
// DEFAULT P-P
	RightHook:
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		0112 ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"RightHookPowered")
		0112 D 1 A_TSP_Melee(24,70,"BasicMeleePuff")
		Goto RightHookEnd
	RightHookPowered:
		0112 D 1 A_TSP_Melee(48,70,"BasicMeleePuff")
		Goto RightHookEnd

// P-P END
	RightHookEnd:
		0112 E 1
		TNT1 A 0 A_GiveInventory("MelPrimaryCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		0112 H 1 A_WeaponReady
		TNT1 A 13 A_WeaponReady
		Goto AfterMelee

//P-P-P
	LeftOverhead:
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		TNT1 A 0 A_PlaySound("*smallmelee", CHAN_VOICE)
		0113 ABC 1 
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"LeftOverheadPowered")
		0113 E 1 A_TSP_Melee(24,70,"BasicMeleePuff")
		Goto LeftOverheadEnd
	LeftOverheadPowered:
		0113 E 1 A_TSP_Melee(48,70,"BasicMeleePuff")
		Goto LeftOverheadEnd
		
// P-P-P End
	LeftOverheadEnd:
		0113 F 1
		TNT1 A 0 A_GiveInventory("MelPrimaryCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 A 11 A_WeaponReady
		TNT1 A 3
		Goto AfterMelee
	
// P-P-P-P
	ScorpionTailFinisher:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"ScorpionTailFinisherPowered")
		TNT1 A 5 A_PlaySound("*strongmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("strongmelee/miss", "SoundSlot5")
		0114 ABC 1
		0114 D 1 A_TSP_Melee(44,90,"UppercutMeleePuff",0,8,16,45,45)
		Goto ScorpionTailFinisherEnd
	ScorpionTailFinisherPowered: // STASIS UPPERCUT EX
		TNT1 A 0
		{
			if (momZ > 0 && momZ <0)
			{
				A_Recoil(-5);

			}
			else
			{
				A_Recoil(-10);
			}
		}
		TNT1 A 8 A_PlaySound("*xlmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("maguppercut", "SoundSlot6")
		TNT1 A 0
		{
			A_SetBlend("124 252 0", 0.2, 5);
			if (momZ > 0 && momZ <0)
			{
				ThrustThingZ(0,25,0,1);
			}
			else
			{
				ThrustThingZ(0,50,0,1);
			}
		}
		0114 ABC 1 BRIGHT
		0114 D 2 BRIGHT A_TSP_Melee(88,90,"MagDivePuff",0,8,16,45,45,TSPM_SUPERFREEZE)
	    TNT1 A 0 ThrustThingZ(0,4,0,1)
		TNT1 A 0 A_SetPitch(pitch - 2, SPF_INTERPOLATE)
	    0114 EEDC 2 BRIGHT ThrustThingZ(0,1,0,1)
	    0114 BA 1 ThrustThingZ(0,1,0,1)
		TNT1 A 0 A_SetPitch(pitch + 2, SPF_INTERPOLATE)
		TNT1 A 10 ThrustThingZ(0,1,0,1)
		Goto AfterMelee

// P-P-P-P End
	ScorpionTailFinisherEnd:
		0114 E 2
		0114 DCBA 1
		TNT1 A 8
		Goto AfterMelee
		
//K-K-P
	KnifeHand:
		TNT1 A 0 A_PlaySoundEX("strongmelee/miss", "SoundSlot5")
		0115 ABC 1 
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"KnifeHandPowered")
		0115 D 1 A_TSP_Melee(30,70,"BasicMeleePuff")
		Goto KnifeHandEnd
	KnifeHandPowered:
		0115 D 1 A_TSP_Melee(60,70,"BasicMeleePuff")
		Goto KnifeHandEnd
		
// K-K-P End
	KnifeHandEnd:
		0115 EFG 1
		TNT1 A 0 A_GiveInventory("MelMachCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		0115 HI 1 A_WeaponReady
		TNT1 A 7 A_WeaponReady
		TNT1 A 5
		Goto AfterMelee
	
//K-K-P-P
	CrossPunch:
		TNT1 A 2 A_PlaySound("*smallmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		011O AB 1 
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"CrossPunchPowered")
		011O C 1 A_TSP_Melee(24,70,"BasicMeleePuff")
		Goto CrossPunchEnd
	CrossPunchPowered:
		011O C 1 A_TSP_Melee(48,70,"BasicMeleePuff")
		Goto CrossPunchEnd

	CrossPunchEnd:
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_GiveInventory("MelMachCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 A 0
		{
			if (CountInv("MelAlternateCombo2") == 1)
			{
				A_GiveInventory("MelAlternateCombo2", 1);
			}
			else
			{
			}
		}
		011O CBA 1 A_WeaponReady
		TNT1 A 0 A_Jump(256,"CrossEnder") //CrossPunch and CrossDuo have the same ending frames, merged into a single state for readability)

	
// K-K-P-P-P
	CrossDuo:
		TNT1 A 1
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		011P AB 1 
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"CrossDuoPowered")
		011P C 1 A_TSP_Melee(24,70,"BasicMeleePuff")
		Goto CrossDuoEnd
	CrossDuoPowered:
		011P C 1 A_TSP_Melee(48,70,"BasicMeleePuff")
		Goto CrossDuoEnd
		
// K-K-P-P-P End
	CrossDuoEnd:
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_GiveInventory("MelMachCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 A 0
		{
			if (CountInv("MelAlternateCombo2") == 2)
			{
				A_GiveInventory("MelAlternateCombo2", 1);
			}
			else
			{
			}
		}
		011P CBA 1 A_WeaponReady
	CrossEnder:
		TNT1 A 12 A_WeaponReady
		TNT1 A 3
		Goto AfterMelee
		
//K-K-P-P-P-P
	RapidFireFinish:
		TNT1 A 1 A_PlaySound("*strongmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("strongmelee/miss", "SoundSlot5")
		011Q ABC 1 
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"RapidFireFinishPowered")
		011Q D 5 A_TSP_Melee(35,90,"StrongMeleePuff")
		Goto RapidFireFinishEnd
	RapidFireFinishPowered:
		011Q D 5 A_TSP_Melee(70,90,"StrongMeleePuff")
		Goto RapidFireFinishEnd

// K-K-P-P-P-P End
	RapidFireFinishEnd:
		011Q EFG 1
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_GiveInventory("MelMachCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 AAAAAA 1 A_WeaponReady
		TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo2", 99)
		TNT1 A 6
		Goto AfterMelee

// KEEP AWAY K-K-P-P-P
	KeepAway:
		TNT1 A 3 A_PlaySound("*strongmelee")
		MLGF A 0 A_PlaySoundEX("*magdrive", "SoundSlot6")
		MLGF ABCDEFGH 1
		TNT1 A 0 A_SetBlend("18 65 32", 0.4, 15)
		MLGF I 1
		{
			A_TSP_Melee(100,90,"BlankMeleePuff", 0,32,8);
			A_SpawnItemEx ("MagPushProjectile",20,0,18,cos(-pitch)*200,0,sin(-pitch)*200,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx ("MagPushProjectileReflective",0,0,18,cos(-pitch)*60,0,sin(-pitch)*60,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx ("MagPushProjectileReflective",0,15,18,cos(-pitch)*60,0,sin(-pitch)*60,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx ("MagPushPainProjectile",20,0,18,cos(-pitch)*200,0,sin(-pitch)*200,0,SXF_NOCHECKPOSITION);
			A_Quake(3,2,0,3,"");
		}
		MLGF HGFE 1 A_Quake(6,2,0,5,"")
		MLGF DCBA 1
		TNT1 A 6
		Goto AfterMelee
		
// Kicks
	AltFire:
		TNT1 A 0 A_JumpIf(CountInv("TSP_Upgrade_KeepAwayCombo")<1,"AltFireCont") // Keep Away Combo not purchased
		TNT1 A 0 A_JumpIf(CountInv("MelAlternateCombo2")>1,"ViperFinisher")
		TNT1 A 0 A_JumpIf(CountInv("MelAlternateCombo2")==1,"AfterMelee")
	AltFireCont:
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo",4,"StingrayFinisher")	// P-P-P-K-K
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo",2,"ViperFinisher")		// K-K-K
		TNT1 A 0 A_JumpIfInventory("MelAlternateCombo",1,"UpperKick")			// K-K
		TNT1 A 0 A_JumpIfInventory("MelPrimaryCombo",3,"AzureKnee")             // P-P-P-K
		TNT1 A 0 A_JumpIfInventory("MelPrimaryCombo",2,"StreetSweeperFinisher") // P-P-K
		
// K DEFAULT
	DefaultKick:
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		011K ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"DefaultKickPowered")
		011K D 1 A_TSP_Melee(24,90,"BasicMeleePuff")
		Goto DefaultKickEnd
	DefaultKickPowered:
		011K D 1 A_TSP_Melee(48,90,"BasicMeleePuff")
		Goto DefaultKickEnd
		
// K END
	DefaultKickEnd:
		011K E 1
		TNT1 A 0 A_GiveInventory("MelAlternateCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		011K FGH 1 A_WeaponReady
		TNT1 A 8 A_WeaponReady
		TNT1 A 4
		{
			if (CountInv("TSP_Upgrade_KeepAwayCombo") == 1)
			{
				A_Refire("SwipeKick");
			}
			else
			{
				A_WeaponReady;
			}
		}
		Goto AfterMelee
		
// K-K
	UpperKick:
		TNT1 A 3
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		TNT1 A 0 A_PlaySound("*smallmelee", CHAN_VOICE)
		011L ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"UpperKickPowered")
		011L D 2 A_TSP_Melee(24,90,"BasicMeleePuff")
		Goto UpperKickEnd
	UpperKickPowered:
		011L D 2 A_TSP_Melee(48,90,"BasicMeleePuff")
		Goto UpperKickEnd
		
// K-K END
	UpperKickEnd:
		011L E 1
		TNT1 A 0 A_GiveInventory("MelAlternateCombo", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		011L F 1 A_WeaponReady
		TNT1 A 9 A_WeaponReady
		TNT1 A 3
		Goto AfterMelee
		
// K-K-K
	ViperFinisher:
		TNT1 A 3 A_PlaySound("*strongmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("strongmelee/miss", "SoundSlot5")
		011R AB 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"ViperFinisherPowered")
		011R C 2 A_TSP_Melee(48,90,"StrongMeleePuff",0,16,8)
		Goto ViperFinisherEnd
		
	ViperFinisherPowered:
		011R C 2 A_TSP_Melee(96,90,"StrongMeleePuff",0,16,8)
		Goto ViperFinisherEnd
	
// K-K-K END
	ViperFinisherEnd:
		011R DEF 1
		TNT1 A 6
		Goto AfterMelee
		
//P-P-K
	StreetSweeperFinisher:
		TNT1 A 4 A_PlaySound("*strongmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("strongmelee/miss", "SoundSlot5")
		011J ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"StreetSweeperFinisherPowered")
		011J D 1 A_TSP_Melee(48,90,"StrongMeleePuff")
		Goto StreetSweeperFinisherEnd
	StreetSweeperFinisherPowered:
		011J D 1 A_TSP_Melee(96,90,"StrongMeleePuff")
		Goto StreetSweeperFinisherEnd
		
// P-P-K End
	StreetSweeperFinisherEnd:
		011J EFGHI 1
		{
			A_SetAngle(angle + 2.0, SPF_INTERPOLATE);
		}
		TNT1 A 5
		Goto AfterMelee
	
// P-P-P-K
	AzureKnee:
		TNT1 A 2
		TNT1 A 0 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		011N ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"AzureKneePowered")
		011N D 3 A_TSP_Melee(24,90,"StrongMeleePuff")
		Goto AzureKneeEnd
	AzureKneePowered:
		011N D 3 A_TSP_Melee(48,90,"StrongMeleePuff")
		Goto AzureKneeEnd
		
// P-P-P-K End
	AzureKneeEnd:
		011N CB 1
		TNT1 A 0 A_GiveInventory("MelAlternateCombo", 4)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		011N A 1 A_WeaponReady
		TNT1 A 8 A_WeaponReady
		TNT1 A 6
    Goto AfterMelee
	
//P-P-P-K-K
	StingRayFinisher:
		TNT1 A 3 A_PlaySound("*strongmelee", CHAN_VOICE)
		TNT1 A 0 A_PlaySoundEX("strongmelee/miss", "SoundSlot5")
		011M ABCD 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"StingRayFinisherPowered")
		011M E 1 A_TSP_Melee(48,90,"StrongMeleePuff")
		Goto StingRayFinisherEnd
	StingRayFinisherPowered:
		011M E 1 A_TSP_Melee(96,90,"StrongMeleePuff")
		Goto StingRayFinisherEnd
		
	StingRayFinisherEnd:
		011M FGHI 1
		TNT1 A 0 A_GiveInventory("MelPistolCombo", 1)
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo2", 99)
		TNT1 A 0 A_TakeInventory("MelMachCombo", 99)
		TNT1 A 5 A_WeaponReady(WRF_NOFIRE)
		Goto AfterMelee
		
//KEEP AWAY K-K
	SwipeKick:
		TNT1 A 2 A_PlaySoundEX("basicmelee/miss", "SoundSlot5")
		011S ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"SwipeKickBerserk")
		011S D 1 A_TSP_Melee(24,90,"BasicMeleePuff",5,0)
		Goto SwipeKickEnd
	SwipeKickPowered:
		011S D 1 A_TSP_Melee(48,90,"BasicMeleePuff",5,0)
		Goto SwipeKickEnd

	SwipeKickEnd:
		TNT1 A 0 A_GiveInventory("MelAlternateCombo2", 1)
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 A 0 A_SetAngle(angle - 5.0, SPF_INTERPOLATE)
		011S E 1 A_SetAngle(angle - 5.0, SPF_INTERPOLATE)
		011S F 1 A_SetAngle(angle - 5.0, SPF_INTERPOLATE)
		011S G 1 A_SetAngle(angle - 5.0, SPF_INTERPOLATE)
		TNT1 A 16 A_WeaponReady
		Goto AfterMelee

	AfterMelee:
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo2", 99)
		TNT1 A 0 A_TakeInventory("MelMachCombo", 99)
		TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
		TNT1 A 0 A_TakeInventory("ChargedAttack", 99)
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
		011D EDCBA 1
		011G A 1
		Goto Ready
	}
}