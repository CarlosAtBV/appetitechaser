//===========================================================================
//
// Makoto Arms Type 919m.0
//
// by Cardboard Marty
// Sprites by Cardboard Marty
//
//===========================================================================

ACTOR MelRetroPistol : TSPMelLightWeapon
{
	Inventory.PickupMessage "$MELRETROPISTOLPICKUP"
	Obituary "%o learned why %k is the king of the iron fist."
	Weapon.Kickback 110
	Weapon.BobStyle Alpha
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 0.7
	Weapon.AmmoType "TSPPistolAmmo"
	Weapon.AmmoGive 0//0
	Weapon.AmmoUse 1
	Inventory.Icon "919PA0"

		TSP_Weapon_Core.AmmoMult 1.35, 1.7, 2.0
		TSP_Weapon_Core.ClipSize 15 //Initial starting size of clip
		TSP_Weapon_Core.FirstPickupLine "*919mpickup" //AAAAGH
		+TSP_Weapon_Core.MODFIREMODE //Activates modfire stuff
		TSP_Weapon_Core.ModTag "Autofire" //Used for "---- On/Off" message when switching mod fire on or off
		-TSP_Weapon_Core.MODFIREZOOMS
		-TSP_Weapon_Core.HOLDDOWNFORMOD

	Tag "$MELRETROPISTOLTAG"
	+NOAUTOFIRE
	+AMMO_OPTIONAL
	+NOEXTREMEDEATH
	+WEAPON.NoAlert
	+WEAPON.CHEATNOTWEAPON
	States
	{
	Spawn:
		919P A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("919m/select", 5)
		TNT1 A 0 A_TakeInventory("MeleeSelected", 1)
		TNT1 A 0 A_TakeInventory("WeaponSelectMemory", 999)
		TNT1 A 0 A_GiveInventory("WeaponSelectMemory", 021)
		TNT1 A 0 A_JumpIfInventory("MelPistolCombo", 1, "PistolCombo")
		TNT1 A 0 A_JumpIfInventory("FullAutoOn", 1, "AutoSelect")
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 3
		MPTU ABCD 1
		MPTG A 1 A_Raise
		Loop
	AutoSelect:
		ASTU EFGH 1
	AutoSelectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		ASTG A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
		TNT1 A 0 A_TakeInventory("MagPushToken", 99)
		TNT1 A 0 A_TakeInventory("MagSlingToken", 99)
		TNT1 A 0 A_JumpIfInventory("ComboCoolDown", 1, "ComboDeselect")
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
		TNT1 A 0 A_PlaySound("919m/holster", 5)
		TNT1 A 0 A_JumpIfInventory("FullAutoOn", 1, "AutoDeselect")
		MPTU DCBA 1
	DeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
		TNT1 A 1 A_Lower
		Loop
	AutoDeselect:
		ASTU DCBA 1
	AutoDeselectLoop:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0  A_Lower
		TNT1 A 1 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMelee")
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitStart")
		MPTG A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_TSP_GotoReadyState
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("MagPushToken", 1,"MagPush")
		TNT1 A 0 A_TSP_JumpIfClipEmpty("Empty")
		MPTF A 0 A_AlertMonsters
		MPTF A 1 A_GunFlash
		MPTF B 0 A_PlaySound("919m/fire", 6)
		MPTF B 0 A_Light2
		MPTF B 1 BRIGHT
		{
			A_TSP_FireBullets(3,2,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		MPTF C 1 BRIGHT A_Light1
		MPTG B 1 A_Light0
		MPTG C 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
		TNT1 A 0 A_GiveInventory("ShotsFiredPewPew", 1)
		MPTG CDEF 1 A_TSP_WeaponReady(WRF_NOBOB)
		"----" A 0 A_TSP_GotoReadyState
		Goto Ready

	ModReady:
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMelee")
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitStart")
		ASTG A 1 A_TSP_WeaponReady(WRF_ALLOWRELOAD)
		"####" A 0 A_TSP_GotoReadyState
		Loop
	ModFire:
		TNT1 A 0 A_TSP_JumpIfClipEmpty("AutoEmpty")
		ASTF A 1 BRIGHT
	AFire0:
		TNT1 A 0 BRIGHT 
		{
			A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_PlaySound("919m/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch - 0.8)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.3,0.3)))
		TNT1 A 0 A_Jump(64,"AFire3")
		TNT1 A 0 A_Jump(88,"Afire2")
	AFire1:
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-4,0,0,0)
		TNT1 A 0 A_Light2
		ASTF B 1 BRIGHT A_Light1
		TNT1 A 0 A_Light0
		TNT1 A 0 A_Refire
		Goto FireEnd
	AFire2:
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-3,0,0,0)
		TNT1 A 0 A_Light2
		ASTF C 1 BRIGHT A_Light1
		TNT1 C 0 A_Light0
		TNT1 C 0 A_Refire
		Goto FireEnd
	AFire3:
		TNT1 A 0 A_FireCustomMissile("9mmCasingSpawner",-5,0,0,0)
		TNT1 A 0 A_Light2
		ASTF D 1 BRIGHT A_Light1
		TNT1 A 0 A_Light0
		TNT1 A 0 A_Refire
	FireEnd:
		TNT1 A 0
		ASTG DCB 1
		Goto ModReady
		
	Switch:
		TNT1 A 0 A_JumpIfInventory("MagSlingToken", 1,"MagSling")
		TNT1 A 0 A_PlaySound("919m/autoon", 5)
		ASTS ABCDE 1
		ASTS F 3
		ASTS GHIJ 1
		ASTG A 1
		ASTS Q 0 A_GiveInventory("FullAutoOn",1)
		Goto ModReady
	ModSwitch:
		TNT1 A 0 A_PlaySound("919m/autooff", 5)
		ASTS QQQPPONMLKJIHG 1
		ASTS F 3
		ASTS EDCBA 1
		ASTS A 0 A_TakeInventory("FullAutoOn", 99)
		ASTS A 1
		Goto Ready
		
	AutoEmpty:
		TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
		ASTF A 5 A_PlaySound("919m/empty", 6)
		"----" A 0 A_TSP_CheckReload()
		Goto ModReady
	Empty:
		TNT1 A 0 A_GiveInventory("NeedstheCock", 1)
		MPTF A 5 A_PlaySound("919m/empty", 6)
		"----" A 0 A_TSP_CheckReload()
		Goto Ready
		
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("FullAutoOn")==0,"ReloadCont")
		ASTG A 1
		ASTS JI 1
	ReloadCont:
		MPTG GHI 1
		TNT1 A 0 A_FireCustomMissile("SpentClip1",3,0,5,0)
		MPTR A 1 A_PlaySound("919m/reloadout", 6)
		MPTR BCD 1
		MPTR EEEEEEEEEEEEEEEEEEEE 1
		TNT1 A 0 A_PlaySound("919m/reloadin1", 6)
		MPTR FGHIJKLMNNNNOP 1
		TNT1 A 0 A_TSP_ReloadAmmo
		MPTR Q 1 A_PlaySound("919m/reloadin2", 5)
		MPTR QRSTUVWX 1
		MPTG A 3
		TNT1 A 0 A_JumpIfInventory("NeedstheCock", 1, "Rechamber")
		TNT1 A 0 A_JumpIfInventory("FullAutoOn", 1, "AutoPistolFinish")
		Goto Ready
	Rechamber:
		MPTC A 0 A_PlaySound("919m/cock", 6)
		MPTC ABCC 1
		MPTC DEFGHIJ 1
		MPTG A 2
		TNT1 A 0 A_TakeInventory("NeedstheCock", 99)
		TNT1 A 0 A_JumpIfInventory("FullAutoOn", 1, "AutoPistolFinish")
		Goto Ready
	AutoPistolFinish:
		ASTS IJ 1
		Goto ModReady
		
	// PISTOL COMBO from Retrograde Mel's P-P-P-K-K String
	PistolCombo:
		TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
		TNT1 A 0 A_TSP_JumpIfClipLower(5,"Select")
		TNT1 A 3 A_PlaySound("basicmelee/miss", 6)
		MPCF ABCD 1
		
		TNT1 A 0  BRIGHT 
		{
			A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_PlaySound("919m/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch - 1.6)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
		
		MPCF EF 1
		TNT1 A 0  BRIGHT 
		{
			A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_PlaySound("919m/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch - 1.6)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
		
		MPCF GH 1
		TNT1 A 0  BRIGHT 
		{
			A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_PlaySound("919m/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch - 1.6)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
		
		MPCF IJ 1
		TNT1 A 0  BRIGHT 
		{
			A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_PlaySound("919m/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch - 1.6)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
		
		MPCF KL 1
		TNT1 A 0  BRIGHT 
		{
			A_TSP_FireBullets(6,6,-1,9,"TSP_BulletPuff",1);
			A_TSP_TakeClip(1);
		}
		TNT1 A 0 A_PlaySound("919m/fire", 5)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch - 1.6)
		TNT1 A 0 A_SetAngle(angle + (frandom(-0.6,0.6)))
		
		MPCF MN 1
		TNT1 A 8
	AfterCombo:
		TNT1 A 0 A_TakeInventory("MelPistolCombo", 99)
		TNT1 A 0 A_TakeInventory("MelPrimaryCombo", 99)
		TNT1 A 0 A_TakeInventory("MelAlternateCombo", 99)
		TNT1 A 0 A_TakeInventory("MelMachCombo", 99)
		TNT1 A 0 A_TakeInventory("FullAutoOn", 99)
		TNT1 A 0 A_GiveInventory("FullAutoOn", 1)
		ASTU ABCD 1
		Goto Ready

	SPTraitStart:
		MPTD ABCD 1
		Goto SPTrait

	//Melee Start
	SPMelee:
		TNT1 A 0 A_JumpIfInventory("FullAutoOn",1,"AutoModeMelee")
		MPTD ABCD 1
	SPMeleeStart:
		TNT1 A 0 A_Jump(128,"Melee2")
		TNT1 A 7 A_WeaponReady (WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
		MPTM ABCDE 1
		ML1F D 0 A_JumpIfInventory("PowerStrength",1,"SPMeleeBerserk")
		MPTM F 1 A_CustomPunch(12,0,0,"StrongMeleePuff",90)
		Goto SPMeleeA
	SPMeleeBerserk:
		MPTM F 1 A_CustomPunch(24,0,0,"StrongMeleePuff",90)
		Goto SPMeleeA
	SPMeleeA:    
		MPTM GHI 1
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 A 6 A_WeaponReady (WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitCombo")
		TNT1 A 0 A_JumpIfInventory("FullAutoOn",1,"AutoMeleeCooldown")
		MPTS ABCD 1
		Goto Ready
	Melee2:
		TNT1 A 7
		TNT1 A 0 A_PlaySound("strongmelee/miss", 5)
		MPTM JKLM 1
		ML1F D 0 A_JumpIfInventory("PowerStrength",1,"Melee2Berserk")
		MPTM N 1 A_CustomPunch(12,0,0,"StrongMeleePuff",90)
		Goto Melee2A
	Melee2Berserk:
		MPTM N 1 A_CustomPunch(24,0,0,"StrongMeleePuff",90)
		Goto Melee2A
	Melee2A: 
		MPTM OPQR 1
		TNT1 A 0 A_GiveInventory("ComboCoolDown", 1)
		TNT1 A 6 A_WeaponReady (WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"SPMeleeStart")
		TNT1 A 0 A_JumpIfInventory("TraitPressed",1,"SPTraitCombo")
	AfterMelee:
		TNT1 A 0 A_TakeInventory("ComboCoolDown", 99)  
		TNT1 A 0 A_TakeInventory("MagPushToken", 99)
		TNT1 A 0 A_TakeInventory("MagSlingToken", 99)
		TNT1 A 0 A_JumpIfInventory("FullAutoOn",1,"AutoMeleeCooldown")
		MPTS ABCD 1
		TNT1 A 0 A_TakeInventory("FullAutoOn", 99)
		TNT1 A 0 A_GiveInventory("FullAutoOn", 1)
		Goto Ready
	//Auto Mode Melee
	AutoModeMelee:
		ASTD ABCD 1
	AutoModeMeleeStart:
		TNT1 A 0 A_Jump(256,"SPMeleeStart")
	AutoMeleeCooldown:
		TNT1 A 0 A_JumpIfInventory("MeleePressed",1,"AutoModeMeleeStart")
		ASTU EFGH 1
		Goto ModReady
	}
}

ACTOR FullAutoOn : Inventory
{
  +IGNORESKILL
  Inventory.MaxAmount 1
}

ACTOR ShotsFiredPewPew : Inventory
{
  +IGNORESKILL
  Inventory.MaxAmount 15
}

ACTOR NeedstheCock : Inventory
{
  +IGNORESKILL
  Inventory.MaxAmount 1
}