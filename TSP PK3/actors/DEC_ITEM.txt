//===========================================================================
// Armor
//
// Sprites by Sangelothi, BloodyAcid, Infirnex, and Cardboard Marty
//===========================================================================

ACTOR TSPGreenArmorPickup : TSP_CustomInventory Replaces GreenArmor
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$SHIELDPICKUP"
	Inventory.PickupSound "armor/lightarmor"
	Inventory.Icon "FLRMA0"
	TSP_CustomInventory.AmountGiven 25 //Used for the pickup message
	States
	{
	Spawn:
		FLRM A 13
		FLRM B 7 Bright
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("TSP_PlayerPawn_Shield", 25)
		Stop
	PickupFail:
		FLRM A 1
		Fail
	}
}


ACTOR TSPBlueArmorPickup : TSP_CustomInventory Replaces BlueArmor
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$SHIELDPICKUP"
	Inventory.PickupSound "armor/heavyarmor"
	Inventory.Icon "PSRMA0"
	TSP_CustomInventory.AmountGiven 50
	States
	{
	Spawn:
		PSRM A 13
		PSRM B 7 Bright
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("TSP_PlayerPawn_Shield", 50)
		Stop
	PickupFail:
		FLRM A 1
		Fail
	}
}

//////////////
// Armor Bonus
//////////////

ACTOR TSPArmorBonusDrop Replaces ArmorBonus
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") == 0,"EasySpawn")
    TNT1 A 0 A_Jump(60, "SpawnRedBonus")
    TNT1 A 0 A_SpawnItemEx("TSPArmorBonus",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  EasySpawn:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("TSPRedArmorBonus",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  SpawnRedBonus:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("TSPRedArmorBonus",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR TSPArmorBonus : TSP_CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$SHIELDPICKUP"
	Inventory.PickupSound "armor/armorshard"
	Inventory.Icon "ARSDD0"
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	TSP_CustomInventory.AmountGiven 1
	States
	{
		Spawn:
			ARSD ABCDCB 6
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("TSP_PlayerPawn_Shield",1)
			Stop
	}
}

ACTOR TSPRedArmorBonus : TSP_CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$SHIELDPICKUP"
	Inventory.PickupSound "armor/armorshard"
	Inventory.Icon "ARSED0"
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	TSP_CustomInventory.AmountGiven 2
	States
	{
		Spawn:
			ARSE ABCDCB 6
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("TSP_PlayerPawn_Shield",2)
			Stop
	}
}

//===========================================================================
// Health
//
// Sprites by Sangelothi, BloodyAcid, zrrion the insect,
// Captain Toenail, and Cardboard Marty
//===========================================================================

ACTOR TSPMegasphere : TSP_CustomInventory replaces Megasphere
{
  Inventory.PickupMessage "\cc[\cnMAX HEALTH\cc] + [\c[Orange]+50 SHIELD\c-\cc]"
  Inventory.PickupSound "health/supercharge"
  +INVENTORY.BIGPOWERUP
  +FLOATBOB
  States
  {
  Spawn:
    SPOT ABCDCB 3 Bright
    Loop
  Pickup:
	TNT1 A 0 A_GiveInventory("TSP_PlayerPawn_Shield", 50)
    TNT1 A 0 A_GiveInventory("MegasphereHealth", 1)
    Stop
  }
}

ACTOR TSPSoulsphere : TSP_CustomInventory replaces Soulsphere
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$HEALTHPICKUP"
	Inventory.PickupSound "health/vitalitysphere"
	+INVENTORY.BIGPOWERUP
	+FLOATBOB
	TSP_CustomInventory.AmountGiven 100
	States
	{
	Spawn:
		LIFE ABCDCB 6 Bright
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("HealthBonus",100)
		Stop
  }
}

ACTOR TSPMedikit : TSP_CustomInventory replaces Medikit
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$HEALTHPICKUP"
	Inventory.PickupSound "health/medkit"
	TSP_CustomInventory.AmountGiven 25
	States
	{
		Spawn:
			MDK1 ABCDEFG 5
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("Health",25)
			Stop
	}
}

ACTOR TSPStimpack : TSP_CustomInventory replaces Stimpack
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$HEALTHPICKUP"
	Inventory.PickupSound "health/stimpack"
	TSP_CustomInventory.AmountGiven 10
	States
	{
		Spawn:
			STPK A 20
			STPK ABC 2
			STPK DDDEEEFFFGGGHHHGGGFFFEEEDDDEEEFFFGGGHHHGGGFFFEEEDDDEEEFFFGGGHHHGGGFFFEEE 1
			STPK CBA 2
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("Health",10)
			Stop
	}
}

///////////////
// Health Bonus
///////////////

ACTOR TSPHealthBonusDrop Replaces HealthBonus
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("TSPGetGameSkill") == 0,"EasySpawn")
    TNT1 A 0 A_Jump(60, "SpawnRedHealth")
    TNT1 A 0 A_SpawnItemEx("TSPHealthBonus",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  EasySpawn:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("TSPRedHealthBonus",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  SpawnRedHealth:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("TSPRedHealthBonus",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR TSPHealthBonus : TSP_CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$HEALTHPICKUP"
	Inventory.PickupSound "health/healthbonus"
	TSP_CustomInventory.AmountGiven 1
	States
	{
		Spawn:
			ASRM ABCDEFG 7
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("HealthBonus",1)
			Stop
	}
}

ACTOR TSPRedHealthBonus : TSP_CustomInventory
{
	Radius 20
	Height 16
	Inventory.PickupMessage "$HEALTHPICKUP"
	Inventory.PickupSound "health/healthbonus"
	TSP_CustomInventory.AmountGiven 2
	States
	{
		Spawn:
			ASRN ABCDEFG 7
			Loop
		Pickup:
			TNT1 A 0 A_GiveInventory("HealthBonus",2)
			Stop
	}
}

ACTOR Over100Health : Inventory
{
    Inventory.MaxAmount 1
    +UNDROPPABLE
}

ACTOR TSPBerserk : Berserk Replaces Berserk
{
  States
  {
  Pickup:
    TNT1 A 0 A_GiveInventory("PowerStrength")
    TNT1 A 0 HealThing(100, 0)
    TNT1 A 0 A_SelectWeapon("MelMelee")
    Stop
  }
}

//===========================================================================
// Miscellaneous
//===========================================================================

//New Barrel that's invulnerable to your punches. YOU'RE WELCOME ERIC.

ACTOR TSPExplosiveBarrel : ExplosiveBarrel Replaces ExplosiveBarrel
{
  DamageFactor "MagDive", 0.0
  DamageFactor "StrongMelee", 0.0
  DamageFactor "Melee", 0.0
  DamageFactor "Uppercut", 0.0
  DamageFactor "Sling", 0.0
  +PUSHABLE
  +SLIDESONWALLS
  +FORCEPAIN
  Obituary "%o went KABOOM! Nice going %o!"
  States
  {
  Pain.Sling:
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_CustomMissile("MagSlingShot", 32, 0)
    Goto Spawn
  Pain.Uppercut:
    TNT1 A 0 A_FaceTarget
	TNT1 A 0 Thrustthing(angle*256/360+192, random(3,4), 0, 0)
	TNT1 A 0 Thrustthingz(0,27,0,1)
    Goto Spawn
  }
}